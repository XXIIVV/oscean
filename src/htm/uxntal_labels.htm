<h2>Uxntal Labels</h2>

<h3 id='structs'>Structs</h3>
<p><b>Enums</b> are labels with unique values that can be used as constants in a
program, they begin by rolling back the program address with
<kbd>|00</kbd>:</p>

<pre>
|00 @Suit &clubs $1 &diamonds $1 &hearts $1 &spades

@is-diamond ( suit -- f )
	.Suit/clubs EQU
	JMP2r
</pre>

<p><b>Structs</b> define padded labels, for example the <code>;person/age</code>
label holds a value of 2, using that offset allows to access specific members
of a data structure by applying the sublabels to a pointer:</p>

<pre>
|00 @Person &name $2 &age $1 &height $2
@members
	=dict/melanye 2a 008c
	=dict/alexane 2c 009a

@get-height ( member* -- height* )
	;Person/height ADD2 LDA2
	JMP2r
</pre>

<p><b>Constants</b> are labels that hold a specific value through the entire execution of the program. They allow to create label that can be used in place of a number, making the code more readable.</p>

<pre>
|1400 @limit

@within-limit ( value* -- f )
	;limit LTH2
	JMP2r
</pre>

<p><b>Pro Tip</b>: Labels can also be used with the padding runes to define a
global length. For example, if one needs to specify a length of 0x30 for multiple
members of a struct, a value can be specified as follow:</p>

<pre>
|30 @length
|00 @struct &field $length
</pre>

<h3 id='scope'>Using and defining scope in Uxntal.</h3>

<p>Uxntal objects are defined statically using a <kbd>@label</kbd> token, it allows for the enclosed methods to access local <kbd>&labels</kbd>. The example below creates an object with the method <kbd>get-x</kbd>, accessible from outside the scope as <kbd>Object/get-x</kbd>. By capitalizing the object name, we communicate to the assembler that the label will not be called and should not raise a warning.</p>

<pre>
@Object

&x $1 &y $1

&get-x <i>( -- x )</i>
	,&x LDR
	JMP2r
</pre>

<p>New methods and members can be appended to an existing scope by creating a label with the scope name followed by a slash and the name of the extension. The <kbd>&labels</kbd> declared within the extension have the same permissions for accessing local labels as during the object definition. To learn more, see <a href='symbols.html'>symbols</a>.</p>

<pre>
@Object/get-y <i>( -- y )</i>
	,&y LDR
	JMP2r
</pre>

<p>When calling local routines the scope's name can be omitted. To see a complete example in that pseudo object-oriented style, see <a href='https://git.sr.ht/~rabbits/uxn/tree/main/item/projects/examples/gui/timer.tal' target='_blank'>timer.tal</a>.</p>

<pre>
&get-both <i>( -- x y )</i>
	Object/get-x 
	/get-y
	JMP2r
</pre>

<h2>Using anonymous functions in Uxntal</h2>

<p>In the context of Uxntal, lambdas is a label designated by a curly bracket
that points to its associated closing bracket, and can be nested. Under the
hood, the opening bracket assembles to the address of the closing bracket which
allows the destination address to be used like any other label such as a JCI
<kbd>?{</kbd>, a JMI, <kbd>!{</kbd> or a plain literal <kbd>;{</kbd>. </p>

<h3>Counted Strings</h3>
<p>Similarly to counted strings, lambdas can encode strings in memory by preceeding their content by the address of the end of the string, so the reading of that string data is not looking for a null byte, but running until reaching the bounds. The advantage is that the address of the next character to append is readily available.</p>

<pre>
@on-reset ( -> )
    ;cstr print-counted
    BRK

@cstr ={ "foo 20 "bar }

@print-counted ( cstr* -- )
    LDA2k SWP2 INC2 INC2
    &l ( -- )
        LDAk #18 DEO
        INC2 GTH2k ?&l
    POP2 POP2 JMP2r
</pre>

<h3>Data Structures</h3>

<p>We can inline a list of items, here's an implementation a function that
returns the member in a list, or <i>nil</i>. Notice how the lambda jump requires the list address to be moved from the return stack.</p>

<pre>
{ =cat =dog =bat } STH2r ;rat member?
</pre>

<pre>
@member? ( {items}* target* -- res* )
	,&t STR2
	DUP2k #0002 SUB2 LDA2 ADD2 SWP2
	&l ( -- )
		LDA2k [ LIT2 &t $2 ] EQU2 ?&found
		INC2 INC2 GTH2k ?&l
	POP2 ;nil &found NIP2
	JMP2r
</pre>

<h3>Unless Blocks</h3>

<p>It is important to notice that a in the case of a conditional jump, the
lambda's content is <b>jumped over when the flag byte is true</b>.</p>

<pre>
.button LDZ ?{ skipped-on-button }
</pre>

<p>Lambdas can also be nested into one another, only the outermost layer of a
nested lambda is evaluated at a time:</p>

<pre>
#01 { { "foo $1 } STH2r !print-lambda } STH2r JCN2
</pre>

<h3>Higher-Order Functions</h3>

<p>A higher-order function is a function that takes a function as an argument or
returns one as a result. In the following example, the foreach routine is
expecting a pointer to a series of bytes, and a pointer to a function to apply
on each byte-long item in memory.</p>

<pre>
{ 01 02 03 04 05 } STH2r ;double foreach
</pre>

<p>The body of the <code>double</code> function reads the value of a cell in
memory and writes a result equal to twice its value, and the body of the
<code>foreach</code> function is merely applying a function to each cell in
memory.</p>

<pre>
@double ( addr* -- addr* )
	STH2k LDAk
	DUP ADD
	STH2r STA
	JMP2r

@foreach ( {bytes}* fn* -- bytes* )
	,&t STR2
	DUP2k #0002 SUB2 LDA2 ADD2 SWP2
	&l ( -- )
		[ LIT2 &t $2 ] JSR2 INC2 GTH2k ?&l
	POP2 POP2
	JMP2r
</pre>





