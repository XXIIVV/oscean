( directory renderer )

|10 @Console &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|000

	@path $40

|100

@on-reset ( -> )
	.Console/type DEI ?{
		;Dict/usage perr #010f DEO
		BRK }
	;on-source-await .Console/vector DEO2
	BRK

@on-source-await ( -> )
	.Console/read DEI
	( ) DUP #20 GTH ?{ POP !on-source-ready }
	[ LIT2 00 &ptr -path ] INCk ,&ptr STR
	STZ2
	BRK

@on-source-ready ( -> )
	;path DUP2 load-dir ;Dict/index OVR2 scap/ <scpy>
	<render>
	#800f DEO
	BRK

(
@|core )

@load-dir ( path* -- )
	.File/name DEO2
	#8000 .File/length DEO2
	;dir .File/read DEO2
	JMP2r

@<render> ( path* -- )
	.File/name DEO2
	( | head )
	;El/<head>
	wstr ;El/<title>
	wstr ;path wstr ;El/</title>
	wstr ;El/</head>
	wstr
	( | body )
	;El/<body>
	wstr ;El/<pre>
	wstr ;dir
	&>w
		write-line LDAk ?&>w
	POP2 ;El/</pre>
	wstr ;El/</body> !wstr

@write-line ( line* -- next* )
	LDA2k [ LIT2 "-- ] EQU2 ?{
		( length ) #0005 write-length
		( link ) write-link INC2 ;El/<br/> !wstr }
	wcap/ INC2 wcap/ INC2 JMP2r

@write-link ( word* -- next* )
	;El/a1 wstr DUP2
	( ) write-word POP2
	( ) ;El/a2 wstr write-word ;El/a3 !wstr

@write-word ( str* -- next* )
	DUP2k wcap/ SWP2 SUB2
	( >> )

@write-length ( str* length* -- next* )
	DUP2 .File/length DEO2
	OVR2 .File/write DEO2
	ADD2 JMP2r

(
@|stdlib )

@wstr ( str* -- next* )
	DUP2 DUP2 scap/ SWP2 SUB2 .File/length DEO2
	.File/write DEO2
	JMP2r

@wcap ( str* -- end* )
	INC2 & LDAk #20 GTH ?wcap
	JMP2r

@scap ( str* -- end* )
	INC2 & LDAk ?scap
	JMP2r

@perr ( err* -- )
	LDAk #19 DEO
	INC2 LDAk ?perr
	POP2 JMP2r

@<scpy> ( src* dst* -- )
	STH2
	&>w
		LDAk #00 STH2kr STA2
		INC2r INC2 LDAk ?&>w
	POP2 POP2r JMP2r

(
@|assets )

@Dict
	&usage "usage: 20 "directory.rom 20 "folder/ 0a $1
	&index "index.html $1

@El
	&<head> "<head> $1
	&</head> "</head> $1
	&<title> "<title> $1
	&</title> "</title> $1
	&<body> "<body> $1
	&</body> "</body> $1 &<br/> 0a $1
	&<pre> "<pre> $1
	&</pre> "</pre> $1
	&a1 "<a 20 "href=' $1
	&a2 "'> $1
	&a3 "</a> $1

@dir

