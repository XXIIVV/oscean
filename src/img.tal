( Subversive )

|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|a0 @File/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|15 @YEAR

|100

@on-reset ( -> )
	diaries/<build>
	BRK

(
@|Framework )

%<lb> ( -- ) {
	#0a18 DEO }

%<ws> ( -- ) {
	#2018 DEO }

%<t> ( tag* -- ) {
	LIT2 "< 18 DEO
	str/<print>
	LIT2 "> 18 DEO }

%</t> ( tag* -- ) {
	LIT2 "< 18 DEO
	LIT2 "/ 18 DEO
	str/<print>
	LIT2 "> 18 DEO }

%<img> ( jpg* -- ) {
	LIT2 "< 18 DEO
	;tag/img str/<print>
	<ws>
	;tag/src str/<print>
	LIT2 "= 18 DEO
	LIT2 "" 18 DEO
	;path/url str/<print>
	;path/diary str/<print>
	str/<print>
	;path/ext-jpg str/<print>
	LIT2 "" 18 DEO
	LIT2 "/ 18 DEO
	LIT2 "> 18 DEO }

%<wrap> ( tag* str* -- ) {
	OVR2 <t>
	str/<print>
	</t> }

(
@|Diary )

%log/has-no-picture ( log* -- log* t ) {
	DUP2 #0006 ADD2 ;dict/no-picture str/cmp3 }

%log/is-muted ( log* -- log* t ) {
	DUP2 #0005 ADD2 LDA LIT ". EQU }

@log/<emit> ( log* -- log* )
	;tag/item <t>
	( | title )
	;tag/title OVR2 #000a ADD2 str/next <wrap>
	( | link )
	;tag/link <t>
	;path/url str/<print>
	;path/diary str/<print>
	DUP2 #0006 ADD2 str/<print>
	;path/ext-jpg str/<print>
	;tag/link </t>
	( | description )
	;tag/description <t>
	;tag/cdata-a str/<print>
	DUP2 #0006 ADD2 <img>
	;tag/cdata-b str/<print>
	;tag/description </t>
	( | pubdate )
	;tag/pubdate <t>
	DUP2 arvelie/year-month-day year-month-day/<print>
	<ws>
	;dict/midnight str/<print>
	;tag/pubdate </t>
	;tag/item </t>
	<lb>
	JMP2r

@diaries/<build> ( -- )
	;dict/xml-head str/<print-ln>
	;dict/rss-version str/<print-ln>
	;tag/channel <t>
	<lb>
	;tag/title ;dict/title <wrap>
	;tag/link ;path/url <wrap>
	;tag/description ;dict/desc <wrap>
	<lb>
	diaries/<parse>
	;tag/channel </t>
	;tag/rss </t>
	JMP2r

@diaries/<parse> ( -- )
	.YEAR
	&>l-pages
		#01 SUB DUP path-table-diary/make workarea/make
		( linecap ) ;workarea/buf #0a00 str/<swap-char>
		&>l-logs
			log/has-no-picture ?{ log/is-muted ?{ log/<emit> } }
			str/cap str/cap str/cap GTH2k ?&>l-logs
		POP2 POP2 DUP ?&>l-pages
	POP JMP2r

(
@|Arvelie )

@arvelie/get-year ( date* -- year* )
	dec/parse #07d6 ADD2 JMP2r

@arvelie/get-month ( char -- res )
	DUP LIT "+ NEQ ?{ POP #1a JMP2r }
	[ LIT "A ] SUB JMP2r

@arvelie/get-doty ( date* -- doty* )
	INC2 INC2 LDAk /get-month STH
	INC2 dec/parse #00 STHr #000e MUL2 ADD2 JMP2r

@arvelie/year-month-day ( date* -- )
	DUP2 /get-doty SWP2 /get-year !doty-year/year-month-day

@year-month-day/<print> ( year* month day -- )
	OVR2 OVR2 year-month-day/dotw dotw/<print>
	LIT2 ", 18 DEO
	<ws>
	#00 SWP dec/<print>
	<ws>
	month/<print>
	<ws> !dec/<print>

@year-month-day/dotw ( year* m d -- dotw )
	( y -= m < 3; ) OVR STH
	SWP2 #00 STHr #02 LTH SUB2 STH2
	( t[m-1] + d ) #00 ROT ;&lut ADD2 LDA #00 SWP ROT #00 SWP ADD2
	( y + y/4 - y/100 + y/400 ) STH2kr STH2kr #02 SFT2 ADD2 STH2kr
	( /100 ) #0064 DIV2 SUB2 STH2r #0190 DIV2 ADD2 ADD2
	( % 7 ) #0007 DIV2k MUL2 SUB2 NIP JMP2r

	&lut [ 00 03 02 05 00 03 05 01 04 06 02 04 ]

@year-month/days ( year* month -- days )
	#00 OVR ;&lut ADD2 LDA SWP #01 NEQ ?{
		STH
		DUP2 year/is-leap STHr ADD }
	NIP NIP JMP2r

	&lut [ 1f 1c 1f 1e 1f 1e 1f 1f 1e 1f 1e 1f ]

@year/is-leap ( year* -- bool )
	DUP2 #0190 DIV2k MUL2 SUB2 #0000 EQU2 ?&leap
	DUP2 #0064 DIV2k MUL2 SUB2 #0000 EQU2 ?&not-leap
	#0003 AND2 #0000 EQU2 JMP2r
	&not-leap POP2 #00 JMP2r
	&leap POP2 #01 JMP2r

@doty-year/year-month-day ( doty* year* -- year* month day )
	,&year STR2
	STH2
	#0c00
	&>l2
		DUP [ LIT2 &year $2 ] ROT year-month/days #00 SWP DUP2 STH2kr GTH2 ?&skip2
		STH2k SUB2r !&continue2
		&skip2 POP2 !{
		&continue2 POP2 INC GTHk ?&>l2 }
	NIP STHr POPr INC ,&year LDR2 SWP2 JMP2r

@dotw/<print> ( dotw -- )
	#00 SWP #20 SFT2 ;&lut ADD2 !str/<print>

	&lut [
	"Sun $1 "Mon $1 "Tue $1 "Wed $1
	"Thu $1 "Fri $1 "Sat $1 ]

@month/<print> ( month -- )
	#00 SWP #20 SFT2 ;&lut ADD2 !str/<print>

	&lut [
	"Jan $1 "Feb $1 "Mar $1 "Apr $1
	"May $1 "Jun $1 "Jul $1 "Aug $1
	"Sep $1 "Oct $1 "Nov $1 "Dec $1 ]

(
@|Utils )

@path-table-diary/make ( id -- path* )
	( x0 ) DUP #0a DIV LIT "0 ADD SWP
	( 0x ) #0a DIVk MUL SUB LIT "0 ADD ,&id STR2
	;&res JMP2r

	&res "src/tables/diary/ &id $2 ".tsv $1

@workarea/make ( path* -- to* from* )
	.File/name DEO2
	#0000 ;&buf SUB2 .File/length DEO2
	;&buf .File/read DEO2
	( clip ) #0000 ;&buf .File/success DEI2 ADD2 STA2
	( range ) ;&buf .File/success DEI2 ADD2 ;&buf JMP2r

(
@|Stdlib )

@dec/<print> ( short* -- )
	[ LIT2r ff00 ]
	&>read
		#000a DIV2k STH2k MUL2 SUB2 STH2r INCr ORAk ?&>read
	POP2
	&>write
		NIP #30 ADD #18 DEO
		OVRr ADDr STHkr ?&>write
	POP2r JMP2r

@dec/parse ( str* -- dec* )
	LDA2
	( a ) [ LIT "0 ] SUB SWP
	( b ) [ LIT "0 ] SUB #0a MUL ADD #00 SWP JMP2r

@str/<print-ln> ( str* -- )
	/<print>
	<lb>
	JMP2r

@str/<print> ( str* -- )
	&>wp
		LDAk DUP #1f GTH ?{ POP POP2 JMP2r }
		#18 DEO
		INC2 !&>wp

@str/cmp3 ( a* b* -- f )
	STH2
	LDAkr LDAk STHr NEQ ?{ INC2r INC2 }
	LDA2r LDA2 STH2r EQU2 JMP2r

@str/next ( str* -- next* )
	&>wn
		LDAk #20 LTH ?{ INC2 !&>wn }
	INC2 JMP2r

@str/<swap-char> ( str* a b -- )
	,&b STR
	,&a STR
	&>wsc
		LDAk [ LIT &a $1 ] NEQ ?{
			STH2k [ LIT &b $1 ] STH2r STA }
		INC2 LDAk ?&>wsc
	POP2 JMP2r

@str/cap ( str* -- end* )
	&>wc
		LDAk ?{ INC2 JMP2r }
		INC2 !&>wc

(
@|Assets )

@dict/xml-head "<?xml 20 "version="1.0" 20 "encoding="UTF-8"?> $1
	&rss-version "<rss 20 "version="2.0"> $1
	&title "XXIIVV $1
	&desc "Journal 20 "Images $1
	&no-picture "000 $1
	&midnight "12:00:00 20 "-0000 $1

@path/url "https://wiki.xxiivv.com/ $1
	&diary "media/diary/ $1
	&ext-jpg ".jpg $1

@tag/channel "channel $1
	&title "title $1
	&link "link $1
	&description "description $1
	&item "item $1
	&img "img $1
	&src "src $1
	&rss "rss $1
	&pubdate "pubDate $1
	&cdata-a "<![CDATA[ $1
	&cdata-b "]]> $1

@workarea/buf

