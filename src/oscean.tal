( The Bequest Globe )

|00 @System &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2 &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|000

	@term &name $16 &parent $16 &diary $6 &pict $4

|100

@on-reset ( -> )
	( | initialize )
	;dict/lexicon-path .File/name DEO2
	#2000 .File/length DEO2
	;lexicon
	( ) DUP2 .File/read DEO2
	( ) DUP2 #0a00 <cswp>
	&>w ( -- )
		INC2 INC2 <init-page>
		<build-page>
		scap/ INC2 LDAk ?&>w
	POP2
	( Halt. ) #800f DEO
	BRK

(
@|build )

@<init-page> ( name* -- name* )
	STH2k ;dict/html-ext ;&buf STH2r <make-path-ext>
	;&site-path .File2/name DEO2
	#0001 .File2/length DEO2
	( cleanup ) #0000 #0100 <mclr>
	DUP2 <load-diary>
	DUP2 <load-incoming>
	( verify orphans ) ( .File/success DEI2 ORA ?{ DUP2 ;err/orphan <print-warning> } ) ( | fill name )
	DUP2 ;term/name <scpy>
	DUP2 find-parent ;term/parent <scpy>
	( | fill diary )
	find-diary DUP2 #ffff EQU2 ?{
		DUP2 ;term/diary <scpy>
		;term/diary [ LIT2 09 00 ] <cswp> }
	POP2 JMP2r

	&site-path [ "site/ &buf $30 ]

@<load-diary> ( name* -- )
	;&buf <scpy>
	;&buf [ LIT2 20 "_ ] <cswp>
	;&log-path .File/name DEO2
	#1000 .File/length DEO2
	;diary DUP2 [ LIT2 &length $2 ] <mclr>
	DUP2 .File/read DEO2
	#0a00 <cswp>
	.File/success DEI2 ,&length STR2
	JMP2r

	&log-path [ "tmp/log- &buf $30 ]

@<load-incoming> ( name* -- )
	;&buf <scpy>
	;&buf [ LIT2 20 "_ ] <cswp>
	;&map-path .File/name DEO2
	#1000 .File/length DEO2
	;incoming DUP2 [ LIT2 &length $2 ] <mclr>
	DUP2 .File/read DEO2
	#0a00 <cswp>
	.File/success DEI2 ,&length STR2
	JMP2r

	&map-path [ "tmp/map- &buf $30 ]

@<build-page> ( name* -- name* )
	( | render )
	;dict/doctype <write-str>
	;dict/html-tag <write-open>
	( | head )
	;dict/head-tag <write-open>
	( | head/meta )
	;dict/meta-head-path <write-inc>
	<write-opengraph>
	( | head/title )
	;dict/title-tag <write-open>
	;dict/title-txt <write-str>
	<space>
	;dict/mdash-entity-txt <write-str>
	<space>
	DUP2 <write-str>
	;dict/title-tag <write-close>
	;dict/head-tag <write-close>
	( | body )
	;dict/body-tag <write-open>
	;dict/meta-header-path <write-inc>
	<build-page-nav>
	<build-page-main>
	;dict/meta-footer-path <write-inc>
	;dict/body-tag <write-close>
	;dict/html-tag !<write-close>

@<build-page-nav> ( name* -- name* )
	;dict/nav-tag <write-open>
	DUP2 #0002 SUB2 LDA chex STHk #00 EQU ?{
		STHkr #02 LTH ?{
			DUP2 find-parent find-parent <build-children>
			POP2 }
		DUP2 find-parent <build-children>
		POP2 }
	POPr <build-children>
	;dict/nav-tag <write-close>
	#0a !<char>

@<build-children> ( name* -- name* )
	;dict/ul-tag <write-open>
	( stash depth ) DUP2k #0002 SUB2 LDA chex STH
	( start after ) scap/ INC2
	&>w ( -- )
		( stop at sibling ) LDAk chex STHkr EQU ?&end
		( when depth+1 ) LDAk chex STHkr INC NEQ ?{
			;dict/li-tag <write-open>
			INC2k INC2 <write-link>
			;dict/li-tag <write-close> }
		INC2 INC2 scap/ INC2 LDAk ?&>w
	&end ( -- )
	POP2 POPr ;dict/ul-tag !<write-close>

@<build-page-main> ( name* -- name* )
	;dict/main-tag <write-open>
	( | picture )
	;term/pict LDA #00 EQU ?{ ;term/diary <build-module-photo> }
	( body ) <build-page-body>
	( module ) <build-page-module>
	( events ) <build-page-events>
	( incoming ) <build-page-incoming>
	( close ) ;dict/main-tag !<write-close>

@<build-page-body> ( name* -- name* )
	STH2k ;dict/htm-ext ;&buf STH2r <make-path-ext>
	;&htm-path !<write-inc>

	&htm-path [ "src/htm/ &buf $30 ]

@<build-page-module> ( term* -- term* )
	DUP2 #0001 SUB2 LDA
	( ) DUP [ LIT ", ] NEQ ?{ POP !<build-module-album> }
	count-diaries #0002 LTH2 ?{ ;term/name ;err/hidden <print-warning> }
	( ) DUP [ LIT "; ] NEQ ?{ POP !<build-module-portal> }
	( ) [ LIT ": ] NEQ ?{ !<build-module-archive> }
	JMP2r

@<build-page-events> ( name* -- name* )
	;dict/ul-tag <write-open>
	;diary
	&>w ( -- )
		DUP2 #0005 ADD2 LDA [ LIT "+ ] NEQ ?{
			;dict/li-tag <write-open>
			DUP2 <build-module-event>
			;dict/li-tag <write-close> }
		scap/ INC2 LDAk ?&>w
	POP2 ;dict/ul-tag !<write-close>

@<build-module-album> ( name* -- name* )
	;diary
	&>w ( -- )
		DUP2 ;dict/no-picture log-has-picture ?{
			DUP2 ;term/pict log-has-picture ?{ DUP2 <build-module-photo> } }
		scap/ INC2 LDAk ?&>w
	POP2 JMP2r

@<build-module-portal> ( -- )
	( stash depth ) DUP2k #0002 SUB2 LDA chex STH
	( start after ) scap/ INC2
	&>w ( -- )
		( stop at sibling ) LDAk chex STHkr EQU ?&end
		( when depth+1 ) LDAk chex STHkr INC NEQ ?{
			INC2k INC2 <load-diary>
			find-diary DUP2 #ffff EQU2 ?{ DUP2 <build-module-photo> }
			POP2 }
		INC2 INC2 scap/ INC2 LDAk ?&>w
	&end ( -- )
	POP2 POPr
	( restore diary ) DUP2 !<load-diary>

@<build-module-archive> ( name* -- name* )
	( stash depth ) DUP2k #0002 SUB2 LDA chex STH
	( start after ) scap/ INC2
	&>w ( -- )
		( stop at sibling ) LDAk chex STHkr EQU ?&end
		( when depth+1 ) LDAk chex STHkr INC NEQ ?{ INC2 INC2 <build-page-body> }
		INC2 INC2 scap/ INC2 LDAk ?&>w
	&end ( -- )
	POP2 POPr JMP2r

@<build-module-event> ( diary* -- )
	( clip bound ) DUP2 #0005 ADD2 STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;dict/code-tag <write-auto>
	( clip restore ) [ LIT "+ ] STH2r STA
	( event spacer ) <space>
	;dict/mdash-entity-txt <write-str>
	<space>
	( event name ) DUP2 #000a ADD2 walk-seg !<write-str>

@<build-module-photo> ( diary* -- )
	LDAk ?{ POP2 JMP2r }
	;dict/figure-tag <write-open>
	( | copy number )
	DUP2 #0006 ADD2
	( > x00 ) LDAk ;&bufa STA
	( > 0xx ) INC2 LDA2 ;&bufb STA2
	;&diary-path
	( + image ) OVR2 #000a ADD2 walk-seg STH2k <write-img>
	;dict/figcaption-tag <write-open>
	;term/name OVR2 #000a ADD2 sseg ?{
		DUP2 extract-term <write-link>
		<space>
		;dict/mdash-entity-txt <write-str>
		<space> }
	( + title ) STH2r <write-str>
	;dict/right-txt ;dict/span-tag <write-open-class>
	( + date ) #0005 <write-len>
	;dict/span-tag <write-close>
	;dict/figcaption-tag <write-close>
	;dict/figure-tag <write-close>
	#0a !<char>

	&diary-path "../media/diary/ &bufa $1 &bufb $2 ".jpg $1

(
@|helpers )

@walk-seg ( str* -- next* )
	INC2 LDAk #1f GTH ?walk-seg
	INC2 JMP2r

@extract-term ( diary* -- term* )
	[ LIT2 00 _&buf ] STR
	#000a ADD2
	( clip bound ) DUP2 #09 next-char STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;&buf scat
	( clip restore ) #09 STH2r STA
	;&buf JMP2r
	&buf $16

@<build-page-incoming> ( -- )
	;incoming LDAk ?{ POP2 JMP2r }
	;dict/incoming-txt ;dict/p-tag <write-open-class>
	;dict/incoming-txt ;dict/b-tag <write-auto>
	<space>
	&>w ( -- )
		DUP2 <write-link>
		<space>
		scap/ INC2 LDAk ?&>w
	POP2 ;dict/p-tag !<write-close>

@<make-path-ext> ( ext* buf* str* -- )
	OVR2 <scpy>
	DUP2 [ LIT2 20 "_ ] <cswp> !scat

(
@|tools )

@find-diary ( name* -- diary* )
	;diary
	&>w ( -- )
		( skip events ) DUP2 ;dict/no-picture log-has-picture ?{ JMP2r }
		scap/ INC2 LDAk ?&>w
	POP2 #ffff JMP2r

@count-diaries ( -- count* )
	[ LIT2r 0000 ] ;diary
	&>w ( -- )
		DUP2 ;dict/no-picture log-has-picture ?{ INC2r }
		scap/ INC2 LDAk ?&>w
	POP2 STH2r JMP2r

@find-parent ( term* -- parent* )
	DUP2 #0002 SUB2 LDA chex ?{ JMP2r }
	#00 prev-char INC2 LDAk chex #01 SUB ,&depth STR
	;lexicon SWP2
	&>l ( -- )
		#0001 SUB2 #00 prev-char INC2k LDA chex [ LIT &depth $1 ] NEQ ?{
			NIP2 INC2 INC2 INC2 JMP2r }
		LTH2k ?&>l
	#0002 JMP2r

@log-has-picture ( log* picture* -- flag )
	SWP2 #0006 ADD2 !scmp3

(
@|framework )

@<write-opengraph> ( name* -- name* )
	DUP2 ;dict/og-title <write-meta-og>
	STH2k ;dict/html-ext ;&buf STH2r <make-path-ext>
	;&path ;dict/og-url <write-meta-og>
	;term/diary LDAk ?{ POP2 ;&default-image ;dict/og-image !<write-meta-og> }
	#0006 ADD2
	( > x00 ) LDAk ,&bufa STR
	( > 0xx ) INC2 LDA2 ,&bufb STR2
	;&diary-path ;dict/og-image !<write-meta-og>

	&diary-path "https://wiki.xxiivv.com/media/diary/ &bufa $1 &bufb $2 ".jpg $1
	&path "https://wiki.xxiivv.com/site/ &buf $30
	&default-image "https://wiki.xxiivv.com/media/services/rss.jpg $1

@<write-inc> ( path* -- )
	DUP2 .File/name DEO2
	#8000 .File/length DEO2
	[ LITr 00 ] ;workarea
	&>s ( -- )
		DUP2 .File/read DEO2
		.File/success DEI2
		( ) DUP2 .File2/length DEO2
		( ) #0000 EQU2 ?{
			DUP2 .File2/write DEO2
			INCr !&>s }
	POP2 STHr ?{ DUP2 ;err/source <print-error> }
	POP2
	( restore ) #0001 .File2/length DEO2
	JMP2r

@<write-img> ( src* alt* -- )
	[ LIT "< ] <char>
	;dict/img-tag <write-str>
	;dict/alt-attr <write-attr>
	;dict/src-attr <write-attr>
	[ LIT "> ] !<char>

@<write-link> ( name* -- )
	;&a <write-str>
	DUP2 [ LIT2 20 "_ ] <cswp>
	DUP2 <write-str>
	DUP2 [ LIT2 "_ 20 ] <cswp>
	;dict/html-ext <write-str>
	[ LIT "" ] <char>
	DUP2 ;term/name scmp #00 EQU ?{ ;dict/self-txt ;dict/class-attr <write-attr> }
	DUP2 ;term/parent scmp #00 EQU ?{ ;dict/parent-txt ;dict/class-attr <write-attr> }
	[ LIT "> ] <char>
	<write-str>
	;dict/a-tag !<write-close>

	&a "<a 20 "href=" $1

@<write-attr> ( body* attr* -- )
	<space>
	<write-str>
	[ LIT2 "=" ] <char2>
	<write-str>
	[ LIT "" ] !<char>

@<write-meta-og> ( content* property* -- )
	[ LIT "< ] <char>
	;dict/meta-tag <write-str>
	;dict/property-attr <write-attr>
	;dict/content-attr <write-attr>
	[ LIT2 "/> ] !<char2>

@<write-open-class> ( tag* class* -- )
	[ LIT "< ] <char>
	<write-str>
	;dict/class-attr <write-attr>
	[ LIT "> ] !<char>

@<write-open> ( tag* -- )
	[ LIT "< ] <char>
	<write-str>
	[ LIT "> ] !<char>

@<write-auto> ( str* tag* -- )
	DUP2 <write-open>
	SWP2 <write-str>
	( >> )

@<write-close> ( tag* -- )
	[ LIT2 "</ ] <char2>
	<write-str>
	[ LIT "> ]
	( >> )

@<char> ( char -- )
	;&b STAk .File2/write DEO2
	POP JMP2r
	&b $1

@<space> ( -- )
	;&b .File2/write DEO2
	JMP2r
	&b 20

@<char2> ( c1 c2 -- )
	,&b STR2
	;&b1 .File2/write DEO2
	;&b2 .File2/write DEO2
	JMP2r
	&b &b1 $1 &b2 $1

@<write-len> ( src* length* -- )
	OVR2 ADD2 SWP2
	&>l ( -- )
		LDAk <char>
		INC2 GTH2k ?&>l
	POP2 POP2 JMP2r

@<write-str> ( addr* -- )
	DUP2 scap/ OVR2 SUB2 .File2/length DEO2
	.File2/write DEO2
	( restore ) #0001 .File2/length DEO2
	JMP2r

@prev-char ( addr* char -- addr* )
	,&t STR
	&>w ( -- )
		LDAk [ LIT &t $1 ] EQU ?{ #0001 SUB2 LDAk ?&>w }
	JMP2r

@next-char ( addr* char -- addr* )
	,&t STR
	&>w ( -- )
		LDAk [ LIT &t $1 ] EQU ?{ INC2 LDAk ?&>w }
	JMP2r

(
@|stdlib )

@chex ( c -- <val> )
	( dec ) LIT "0 SUB DUP #09 GTH [ JMP JMP2r ]
	( hex ) #27 SUB DUP #0f GTH [ JMP JMP2r ]
	( err ) POP #ff JMP2r

@<cswp> ( str* a b -- )
	,&b STR
	,&a STR
	&>w ( -- )
		LDAk [ LIT &a $1 ] NEQ ?{
			STH2k [ LIT &b $1 ] STH2r STA }
		INC2 LDAk ?&>w
	POP2 JMP2r

@scap ( str* -- end* )
	INC2 & LDAk ?scap
	JMP2r

@scat ( src* dst* -- )
	scap/

@<scpy> ( src* dst* -- )
	STH2
	&>w ( -- )
		LDAk #00 STH2kr STA2
		INC2r INC2 LDAk ?&>w
	POP2 POP2r JMP2r

@scmp3 ( a* b* -- f )
	STH2
	LDAkr LDAk STHr NEQ ?{ INC2r INC2 }
	LDA2r LDA2 STH2r EQU2 JMP2r

@scmp ( a* b* -- f )
	STH2
	&>l ( -- )
		LDAk ?{ &d LDA LDAr STHr EQU JMP2r }
		LDAk LDAkr STHr NEQ ?&d
		INC2 INC2r !&>l

@sseg ( a* b* -- f )
	STH2
	&>l ( -- )
		LDAk LDAkr STHr NEQ ?{
			INC2k LDA #00 EQU ?{ INC2 INC2r !&>l } }
	LDA LDAr STHr EQU JMP2r

@<mclr> ( dst* len* -- )
	,&length STR2
	,&dst STR2
	;&mmu .System/expansion DEO2
	JMP2r

	&mmu [ 00 &length $2 0000 &dst $2 00 ]

@<print-hex> ( hex* -- )
	SWP <print-hex>/b
	&b ( b -- )
	DUP #04 SFT <print-hex>/c
	&c ( c -- )
	#0f AND DUP #09 GTH #27 MUL ADD #30 ADD #18 DEO
	JMP2r

@<print-str> ( str* -- )
	LDAk #18 DEO
	INC2 & LDAk ?<print-str>
	POP2 JMP2r

@<print-warning> ( name* msg* -- )
	( msg ) <print-str>/
	( name ) <print-str>/
	#0a18 DEO
	JMP2r

@<print-error> ( name* msg* -> )
	( msg ) <print-str>/
	( name ) <print-str>/
	#0a18 DEO
	#010f DEO
	BRK

(
@|dict )

@dict
	&meta-head-path "src/htm/meta_head.htm $1
	&meta-header-path "src/htm/meta_header.htm $1
	&meta-footer-path "src/htm/meta_footer.htm $1
	&lexicon-path "src/tables/lexicon $1
	&self-txt "self $1
	&mdash-entity-txt "&mdash; $1
	&parent-txt "parent $1
	&title-txt "XXIIVV $1
	&class-attr "class $1
	&html-ext ".html $1
	&htm-ext ".htm $1
	&doctype "<!DOCTYPE 20 "html> $1
	&html-tag "html $1
	&head-tag "head $1
	&meta-tag "meta $1
	&title-tag "title $1
	&body-tag "body $1
	&nav-tag "nav $1
	&main-tag "main $1
	&p-tag "p $1
	&b-tag "b $1
	&ul-tag "ul $1
	&li-tag "li $1
	&a-tag "a $1
	&span-tag "span $1
	&img-tag "img $1
	&code-tag "code $1
	&figure-tag "figure $1
	&figcaption-tag "figcaption $1
	&og-title "og:title $1
	&og-image "og:image $1
	&og-url "og:url $1
	&src-attr "src $1
	&alt-attr "alt $1
	&property-attr "property $1
	&content-attr "content $1
	&no-picture "000 $1
	&incoming-txt "incoming $1
	&right-txt "right $1

@err
	&source "-- 20 "Source: 20 $1
	&orphan "-- 20 "Orphan: 20 $1
	&hidden "-- 20 "Hidden: 20 $1

(
@|memory )

@lexicon $2000

@diary $1000

@incoming $1000

@workarea

