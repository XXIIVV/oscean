( The Bequest Globe )

|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|a0 @File/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|000

	@term/name $16 &parent $16 &diary $6 &pict $4 &cap

|100

@on-reset ( -> )
	( | initialize )
	;dict/lexicon-path .File/name DEO2
	#2000 .File/length DEO2
	;lexicon/buf
	( ) DUP2 .File/read DEO2
	( ) DUP2 #0a00 str/<swap-char>
	&>w
		INC2 INC2 <init-page>
		#0001 .File2/length DEO2
		<build-page>
		str/cap LDAk ?&>w
	POP2
	( Halt. ) #800f DEO
	BRK

(
@|build )

%log/get-rune ( log* -- log* rune ) {
	DUP2 #0005 ADD2 LDA }

%log/has-picture ( log* picture* -- flag ) {
	SWP2 #0006 ADD2 str/cmp3 }

%log/has-no-picture ( log* -- log* t ) {
	DUP2 ;dict/no-picture log/has-picture }

@<init-page> ( name* -- name* )
	DUP2 site-path-rel/set .File2/name DEO2
	( | cleanup )
	#0000 ;term/cap mem/<clr>
	DUP2 diary/<load>
	DUP2 incoming/<load>
	( | verify orphans )
	.File/success DEI2 ORA ?{ DUP2 ;err/orphan <print-warning> }
	( | fill name )
	DUP2 ;term/name str/<copy>
	DUP2 find-parent ;term/parent str/<copy>
	( | fill diary )
	diary/find DUP2 #ffff EQU2 ?{
		DUP2 ;term/diary str/<copy>
		;term/diary [ LIT2 09 00 ] str/<swap-char> }
	POP2 JMP2r

@diary/<load> ( name* -- )
	/set-path .File/name DEO2
	#1000 .File/length DEO2
	;diary/buf DUP2 [ LIT2 &length $2 ] mem/<clr>
	DUP2 .File/read DEO2
	#0a00 str/<swap-char>
	.File/success DEI2 ,&length STR2
	JMP2r

@diary/find ( -- diary* )
	;&buf
	&>wf
		log/has-no-picture ?{ JMP2r }
		str/cap LDAk ?&>wf
	POP2 #ffff JMP2r

@diary/count ( -- count* )
	[ LIT2r 0000 ] ;diary/buf
	&>wc
		log/has-no-picture ?{ INC2r }
		str/cap LDAk ?&>wc
	POP2 STH2r JMP2r

@incoming/<load> ( name* -- )
	/set-path .File/name DEO2
	#1000 .File/length DEO2
	;&buf DUP2 [ LIT2 &length $2 ] mem/<clr>
	DUP2 .File/read DEO2
	#0a00 str/<swap-char>
	.File/success DEI2 ,&length STR2
	JMP2r

@incoming/set-path ( name* -- buf* )
	;&resbuf ,&ptr STR2
	;&res SWP2
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		#00 [ LIT2 &ptr =&resbuf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w

	&res [ "tmp/map- &resbuf $30 ]

(
@|Html )

%html/<length> ( src* length* -- ) {
	.File2/length DEO2
	.File2/write DEO2
	#0001 .File2/length DEO2 }

%html/<quoted> ( str* -- ) {
	[ LIT2r =ascii/quote LITr -File2/write ] DEO2kr
	html/<str>
	DEO2r }

%html/<attribute> ( body* attr* -- ) {
	;ascii/space .File2/write DEO2
	html/<str>
	;ascii/equal .File2/write DEO2
	html/<quoted> }

%html/<open> ( tag* -- ) {
	;ascii/lt .File2/write DEO2
	html/<str>
	;ascii/gt .File2/write DEO2 }

%html/<open-class> ( class* tag* -- ) {
	;ascii/lt .File2/write DEO2
	html/<str>
	;dict/class-attr html/<attribute>
	;ascii/gt .File2/write DEO2 }

%html/<close> ( tag* -- ) {
	;ascii/lt .File2/write DEO2
	;ascii/slash .File2/write DEO2
	html/<str>
	;ascii/gt .File2/write DEO2 }

%html/<auto> ( str* tag* -- ) {
	DUP2 html/<open>
	SWP2 html/<str>
	html/<close> }

%html/<img> ( src* alt* -- ) {
	;ascii/lt .File2/write DEO2
	;tag/img html/<str>
	;dict/alt-attr html/<attribute>
	;dict/src-attr html/<attribute>
	;ascii/gt .File2/write DEO2 }

%html/<str> ( addr* -- ) {
	DUP2 str/scap-1 OVR2 SUB2 html/<length> }

%html/<meta-og> ( content* property* -- ) {
	;ascii/lt .File2/write DEO2
	;tag/meta html/<str>
	;dict/property-attr html/<attribute>
	;dict/content-attr html/<attribute>
	;ascii/slash .File2/write DEO2
	;ascii/gt .File2/write DEO2 }

(
@|Build )

@<build-page> ( name* -- name* )
	;dict/doctype html/<str>
	;tag/html html/<open>
	;tag/head html/<open>
	;dict/meta-head-path <build-include>
	<write-opengraph>
	;tag/title html/<open>
	;dict/xxiivv-txt html/<str>
	;dict/mdash-entity-txt html/<str>
	DUP2 html/<str>
	;tag/title html/<close>
	;tag/head html/<close>
	;tag/body html/<open>
	;dict/meta-header-path <build-include>
	;tag/nav html/<open>
	DUP2 #0002 SUB2 LDA chr/hex STHk #00 EQU ?{
		STHkr #02 LTH ?{
			DUP2 find-parent find-parent <build-children>
			POP2 }
		DUP2 find-parent <build-children>
		POP2 }
	POPr <build-children>
	;tag/nav html/<close>
	;ascii/line .File2/write DEO2
	;tag/main html/<open>
	;term/diary <build-module-photo>
	DUP2 <build-include>
	<build-page-module>
	<build-page-events>
	<build-page-incoming>
	;tag/main html/<close>
	;dict/meta-footer-path <build-include>
	;tag/body html/<close>
	;tag/html html/<close>
	JMP2r

@<build-children> ( name* -- name* )
	;tag/ul html/<open>
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{
			;tag/li html/<open>
			INC2k INC2 <write-link>
			;tag/li html/<close> }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr ;tag/ul html/<close>
	JMP2r

@<build-page-module> ( term* -- term* )
	DUP2 #0001 SUB2 LDA
	( ) DUP [ LIT ", ] NEQ ?{ POP !<build-module-album> }
	diary/count #0002 LTH2 ?{ ;term/name ;err/hidden <print-warning> }
	( ) DUP [ LIT "; ] NEQ ?{ POP !<build-module-portal> }
	( ) DUP [ LIT ". ] NEQ ?{ POP !<build-module-both> }
	( ) [ LIT ": ] NEQ ?{ !<build-module-archive> }
	JMP2r

@<build-page-events> ( name* -- name* )
	;tag/ul html/<open>
	;diary/buf
	&>w
		log/get-rune [ LIT "+ ] NEQ ?{
			;tag/li html/<open>
			DUP2 <build-module-event>
			;tag/li html/<close> }
		str/cap LDAk ?&>w
	POP2 ;tag/ul html/<close>
	JMP2r

@<build-page-incoming> ( -- )
	;incoming/buf LDAk ?{ POP2 JMP2r }
	;tag/p html/<open>
	;dict/incoming-txt ;tag/b html/<auto>
	[ LIT2r =&null ]
	&>w
		DUP2 STH2kr str/cmp ?{
			DUP2 <write-link>
			;ascii/space .File2/write DEO2
			POP2r STH2k }
		str/cap LDAk ?&>w
	POP2 POP2r ;tag/p html/<close>
	JMP2r
	&null ffff

@<build-module-album> ( name* -- name* )
	;diary/buf
	&>w
		log/has-no-picture ?{
			DUP2 ;term/pict log/has-picture ?{ DUP2 <build-module-photo> } }
		str/cap LDAk ?&>w
	POP2 JMP2r

@<build-module-portal> ( -- )
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{
			INC2k INC2 diary/<load>
			diary/find DUP2 #ffff EQU2 ?{ DUP2 <build-module-photo> }
			POP2 }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr
	( restore diary ) DUP2 !diary/<load>

@<build-module-archive> ( name* -- name* )
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{ INC2 INC2 DUP2 <build-include> }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr JMP2r

@<build-module-both> ( name* -- name* )
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{
			INC2k INC2 diary/<load>
			diary/find DUP2 #ffff EQU2 ?{ DUP2 <build-module-photo> }
			POP2 }
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{ INC2 INC2 DUP2 <build-include> }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr JMP2r

@<build-module-event> ( diary* -- )
	( clip bound ) DUP2 #0005 ADD2 STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;tag/code html/<auto>
	( clip restore ) [ LIT "+ ] STH2r STA
	;dict/mdash-entity-txt html/<str>
	( event name ) DUP2 #000a ADD2 walk-seg html/<str>
	JMP2r

@<build-module-photo> ( diary* -- )
	LDAk ?{ POP2 JMP2r }
	;tag/figure html/<open>
	DUP2 diary-path-rel/get OVR2 #000a ADD2 walk-seg STH2k html/<img>
	;tag/figcaption html/<open>
	;term/name OVR2 #000a ADD2 str/seg ?{
		DUP2 #000a ADD2 substr/set <write-link>
		;dict/mdash-entity-txt html/<str> }
	STH2r html/<str>
	;dict/right-txt ;tag/span html/<open-class>
	#0005 html/<length>
	;tag/span html/<close>
	;tag/figcaption html/<close>
	;tag/figure html/<close>
	;ascii/line .File2/write DEO2
	JMP2r

@<build-include> ( path* -- )
	htm-path/set DUP2 .File/name DEO2
	#8000 .File/length DEO2
	[ LITr 00 ] ;workarea/buf
	&>s
		DUP2 .File/read DEO2
		.File/success DEI2
		( ) DUP2 .File2/length DEO2
		( ) #0000 EQU2 ?{
			DUP2 .File2/write DEO2
			INCr !&>s }
	POP2 STHr ?{ DUP2 ;err/source <print-error> }
	POP2
	( restore ) #0001 .File2/length DEO2
	JMP2r

(
@|helpers )

@prev-nil ( addr* -- addr* )
	&>w
		LDAk ?{ JMP2r }
		#0001 SUB2 !&>w

@walk-seg ( str* -- next* )
	&>w
		INC2 LDAk #1f GTH ?&>w
	INC2 JMP2r

@find-parent ( term* -- parent* )
	DUP2 #0002 SUB2 LDA chr/hex ?{ JMP2r }
	prev-nil INC2 LDAk chr/hex #01 SUB ,&depth STR
	;lexicon/buf SWP2
	&>l
		#0001 SUB2 prev-nil INC2k LDA chr/hex [ LIT &depth $1 ] NEQ ?{
			NIP2 INC2 INC2 INC2 JMP2r }
		LTH2k ?&>l
	#0002 JMP2r

@<write-opengraph> ( name* -- name* )
	DUP2 ;dict/og-title html/<meta-og>
	DUP2 site-path-abs/set ;dict/og-url html/<meta-og>
	;term/diary LDAk ?{
		POP2 ;&default-image ;dict/og-image html/<meta-og>
		JMP2r }
	diary-path-abs/get ;dict/og-image html/<meta-og>
	JMP2r

	&default-image "https://wiki.xxiivv.com/media/ "services/rss.jpg $1

@<write-link> ( name* -- )
	;&a html/<str>
	DUP2 path/set html/<str>
	;ascii/quote .File2/write DEO2
	DUP2 ;term/name str/cmp #00 EQU ?{ ;dict/self-txt ;dict/class-attr html/<attribute> }
	DUP2 ;term/parent str/cmp #00 EQU ?{ ;dict/parent-txt ;dict/class-attr html/<attribute> }
	;ascii/gt .File2/write DEO2
	html/<str>
	;tag/a html/<close>
	JMP2r

	&a "<a 20 "href=" $1

@<print-warning> ( name* msg* -- )
	( msg ) str/<print>
	( name ) str/<print>
	#0a18 DEO
	JMP2r

@<print-error> ( name* msg* -> )
	( msg ) str/<print>
	( name ) str/<print>
	#0a18 DEO
	#010f DEO
	BRK

~src/include.tal

