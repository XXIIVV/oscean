( The Bequest Globe )

|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|a0 @File/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|000

	@path/buf $30
	@term/name $16 &parent $16 &diary $6 &pict $4 &entry $20 &cap $1

|100

@on-reset ( -> )
	#800f DEO
	journal/<delete>
	sitemap/<delete>
	lexicon/<load>
	sitemap/<build>
	diary/<parse>
	lexicon/<build>
	diaries/<print-available>
	#010e DEO
	BRK

@term/<clear> ( -- )
	[ LIT2 -&cap -&name ]
	&>l
		#00 OVR STZ
		INC GTHk ?&>l
	POP2 JMP2r

@term/<set-name> ( name* -- )
	[ LIT2 -&name _&name-ptr ] STR
	&>wn
		LDAk DUP ?{ POP POP2 JMP2r }
		[ LIT2 00 &name-ptr -&name ] INCk ,&name-ptr STR
		STZ2
		INC2 !&>wn

@term/<set-parent> ( name* -- )
	[ LIT2 -&parent _&parent-ptr ] STR
	&>wp
		LDAk DUP ?{ POP POP2 JMP2r }
		[ LIT2 00 &parent-ptr -&parent ] INCk ,&parent-ptr STR
		STZ2
		INC2 !&>wp

@term/<set-diary> ( diary* -- )
	INC2k ORA ?{ POP2 JMP2r }
	[ LIT2 -&diary _&diary-ptr ] STR
	&>wd
		LDAk DUP ?{ POP POP2 JMP2r }
		[ LIT2 00 &diary-ptr -&diary ] INCk ,&diary-ptr STR
		STZ2
		INC2 !&>wd

(
@|Html )

%html/<lb> { ;ascii/line .File2/write DEO2 }

%html/<ws> { ;ascii/space .File2/write DEO2 }

%html/<str> ( addr* -- ) {
	DUP2 str/tail OVR2 SUB2 html/<length> }

%html/<length> ( src* length* -- ) {
	.File2/length DEO2
	.File2/write DEO2
	#0001 .File2/length DEO2 }

%html/<attribute> ( body* attr* -- ) {
	;ascii/space .File2/write DEO2
	html/<str>
	;ascii/equal .File2/write DEO2
	[ LIT2r =ascii/quote LITr -File2/write ] DEO2kr
	html/<str>
	DEO2r }

%html/<open> ( tag* -- ) {
	;ascii/lt .File2/write DEO2
	html/<str>
	;ascii/gt .File2/write DEO2 }

%html/<open-class> ( class* tag* -- ) {
	;ascii/lt .File2/write DEO2
	html/<str>
	;attr/class html/<attribute>
	;ascii/gt .File2/write DEO2 }

%html/<close> ( tag* -- ) {
	;ascii/lt .File2/write DEO2
	;ascii/slash .File2/write DEO2
	html/<str>
	;ascii/gt .File2/write DEO2 }

%html/<auto> ( str* tag* -- ) {
	DUP2 html/<open>
	SWP2 html/<str>
	html/<close> }

@html/<link> ( str* -- )
	;&l-a html/<str>
	DUP2 path/make html/<str>
	;ascii/quote .File2/write DEO2
	;ascii/gt .File2/write DEO2
	html/<str>
	;tag/a html/<close>
	JMP2r

	&l-a "<a 20 "href=" $1

@html/<local> ( name* -- )
	;&l-a html/<str>
	DUP2 path/make html/<str>
	;ascii/quote .File2/write DEO2
	DUP2 ;term/name str/cmp #00 EQU ?{ ;dict/self-txt ;attr/class html/<attribute> }
	DUP2 ;term/parent str/cmp #00 EQU ?{ ;dict/parent-txt ;attr/class html/<attribute> }
	;ascii/gt .File2/write DEO2
	html/<str>
	;tag/a html/<close>
	JMP2r

@html/<photo> ( diary* self -- )
	STH
	LDAk ?{ POP2 POPr JMP2r }
	;tag/figure html/<open>
	( | photo )
	;&a html/<str>
	DUP2 #000a ADD2 DUP2 str/next html/<str>
	;&b html/<str>
	OVR2 #0006 ADD2 #0003 html/<length>
	;&c html/<str>
	( | caption )
	;tag/figcaption html/<open>
	STHr ?{
		DUP2 substr/set html/<link>
		;dict/spacer html/<str> }
	str/next html/<str>
	;dict/right-txt ;tag/span html/<open-class>
	#0005 html/<length>
	;tag/span html/<close>
	;tag/figcaption html/<close>
	;tag/figure html/<close>
	JMP2r

	&a "<img 20 "alt=" $1
	&b "" 20 "src="../media/diary/ $1
	&c ".jpg"> $1

@<write-opengraph> ( name* -- name* )
	DUP2 ;og/title /<write>
	DUP2 path-abs-site/set ;og/url /<write>
	;term/diary LDAk ?{
		POP2 ;path/default-image ;og/image /<write>
		JMP2r }
	path-abs-diary/get ;og/image
	( >> )
	&<write> ( content* property* -- )
	;ascii/lt .File2/write DEO2
	;tag/meta html/<str>
	;attr/property html/<attribute>
	;attr/content html/<attribute>
	;ascii/slash .File2/write DEO2
	;ascii/gt .File2/write DEO2
	JMP2r

(
@|build )

%log/get-rune ( log* -- log* rune ) {
	DUP2 #0005 ADD2 LDA }

%log/has-picture ( log* picture* -- flag ) {
	SWP2 #0006 ADD2 str/cmp3 }

%log/has-no-picture ( log* -- log* t ) {
	DUP2 ;dict/no-picture log/has-picture }

@diary/<clear> ( -- )
	;&buf [ LIT2 &length $2 ] mem/<clr>
	JMP2r

@diary/<load> ( name* -- )
	/<clear>
	path-tmp-log/set-path .File/name DEO2
	#1000 .File/length DEO2
	;&buf DUP2 .File/read DEO2
	#0a00 str/<swap-char>
	.File/success DEI2 ,&length STR2
	JMP2r

@diary/find ( -- diary* )
	;&buf
	&>wf
		log/has-no-picture ?{ JMP2r }
		str/cap LDAk ?&>wf
	POP2 #ffff JMP2r

@diary/count ( -- count* )
	[ LIT2r 0000 ] ;&buf
	&>wc
		log/has-no-picture ?{ INC2r }
		str/cap LDAk ?&>wc
	POP2 STH2r JMP2r

@diary/<parse> ( -- )
	;path/events .File2/name DEO2
	[ LIT2 01 -File2/delete ] DEO
	;header-txt html/<str>
	;tag/p html/<open>
	#4000 .File/length DEO2
	;diary-path-pages/end ;diary-path-pages
	&>l-pages
		LDA2k workarea/load-file
		( uncap ) ;workarea/buf #0a00 str/<swap-char>
		&>l-logs
			log/parse GTH2k ?&>l-logs
		POP2 POP2 INC2 INC2 GTH2k ?&>l-pages
	POP2 POP2 ;tag/p html/<close>
	JMP2r

@diaries/get ( id* -- mask )
	( bit ) #01 OVR #07 AND #40 SFT SFT STH
	( res ) #03 SFT2 ;&buf ADD2 LDA STHr AND JMP2r

@diaries/<use> ( log* -- )
	( | validate photo id )
	#0006 ADD2 str/to-dec ORAk ?{ POP2 JMP2r }
	DUP2 /get ?{
		( bit ) #01 OVR #07 AND #40 SFT SFT STH
		( res ) #03 SFT2 ;&buf ADD2 LDAk STHr ORA ROT ROT STA
		JMP2r }
	;dict/warn-duplicate str/<print>
	dec/<print>
	#0a18 DEO
	JMP2r

@diaries/next ( -- id* )
	#0001
	&>w
		DUP2 /get ?{ JMP2r }
		INC2 !&>w

@diaries/<print-available> ( -- )
	;dict/available-txt str/<print>
	/next dec/<print>
	#0a18 DEO
	JMP2r

(
@|Lexicon )

@lexicon/<load> ( -- )
	;path/lexicon .File/name DEO2
	#2000 .File/length DEO2
	;&buf
	( ) DUP2 .File/read DEO2
	( ) #0a00 !str/<swap-char>

@lexicon/<build> ( -- )
	;&buf
	&>wb
		INC2 INC2 page/build str/cap LDAk ?&>wb
	POP2 JMP2r

@lexicon/find ( name* -- term* )
	,&t STR2
	;&buf
	&>w
		INC2 INC2 DUP2 [ LIT2 &t $2 ] str/cmp ?{
			str/cap LDAk ?&>w
		POP2 #ffff }
	JMP2r

@lexicon/<parse> ( -- )
	;&buf
	( range ) .File/success DEI2 ADD2k NIP2 SWP2
	&>lp
		INC2 INC2 DUP2 DUP2 ,&src STR2
		path-src-htm/set workarea/load-file
		&>ll
			LDA2k [ LIT2 "hr ] NEQ2 ?{
				INC2k INC2 LDA2 [ LIT2 "ef ] NEQ2 ?{
					[ LIT2 &src $2 ] OVR2 #0006 ADD2 link/<parse> } }
			INC2 GTH2k ?&>ll
		POP2 POP2
		( next ) str/cap GTH2k ?&>lp
	POP2 POP2 JMP2r

@lexicon/<write-children-rec> ( addr* -- next* )
	( | take depth )
	LDAk STH
	( | name )
	;tag/li html/<open>
	INC2 INC2 DUP2 html/<link>
	;tag/li html/<close>
	html/<lb>
	str/cap
	( | children )
	LDAk STHkr INC NEQ ?{
		;tag/ul html/<open>
		/<write-children-rec>
		;tag/ul html/<close> }
	( | sibling )
	LDAk STHkr EQU ?{ POPr JMP2r }
	POPr !/<write-children-rec>

@lexicon/find-parent ( term* -- parent* )
	DUP2 #0002 SUB2 LDA chr/hex ?{ JMP2r }
	str/head INC2 LDAk chr/hex #01 SUB ,&depth STR
	;&buf SWP2
	&>lf
		#0001 SUB2 str/head INC2k LDA chr/hex [ LIT &depth $1 ] NEQ ?{
			NIP2 INC2 INC2 INC2 JMP2r }
		LTH2k ?&>lf
	#0002 JMP2r

(
@|Link )

%link/is-anchor ( path* -- path* t ) {
	LDAk [ LIT "# ] EQU }

%link/is-relative ( path* -- path* t ) {
	LDAk [ LIT ". ] EQU }

@link/is-external ( path* -- path* t )
	LDA2k [ LIT2 "ht ] NEQ2 ?{
		INC2k INC2 LDA2 [ LIT2 "tp ] EQU2 JMP2r }
	#00 JMP2r

@link/has-html-ext ( str* -- t )
	LDA2k [ LIT2 ".h ] NEQ2 ?{
		INC2 INC2 LDA2k [ LIT2 "tm ] NEQ2 ?{
			INC2 INC2 LDA [ LIT "l ] EQU JMP2r } }
	POP2 #00 JMP2r

@link/<print-err> ( src* link* -- src* link* )
	#010f DEO
	;dict/err-redlink str/<print>
	;path-tmp-map/resbuf str/<print>
	;dict/in str/<print>
	DUP2 !str/<print-ln>

@link/<parse> ( src* link* -- )
	link/is-relative ?{
		link/is-anchor ?{
			link/is-external ?{
				path-tmp-map/set
				( | handlers )
				;path-tmp-map/resbuf lexicon/find INC2 ORA ?{ /<print-err> }
				/has-html-ext ?{ /<print-err> }
				;path-tmp-map/resbuf [ LIT2 20 "_ ] str/<swap-char>
				;path-tmp-map/res .File2/name DEO2
				html/<str>
				html/<lb>
				JMP2r } } }
	POP2 POP2 JMP2r

(
@|Build )

@page/build ( name* -- name* )
	DUP2 path-site/set .File2/name DEO2
	( | cleanup )
	term/<clear>
	DUP2 diary/<load>
	DUP2 incoming/<load>
	( | verify orphans )
	.File/success DEI2 ORA ?{ DUP2 ;err/orphan warning/<print> }
	( | fill name )
	DUP2 term/<set-name>
	DUP2 lexicon/find-parent term/<set-parent>
	diary/find term/<set-diary>
	( recover ) #0001 .File2/length DEO2
	( | build )
	;dict/doctype html/<str>
	;tag/html html/<open>
	;tag/head html/<open>
	;path/meta-head <build-include>
	<write-opengraph>
	;tag/title html/<open>
	;dict/xxiivv-txt html/<str>
	;dict/mdash-entity-txt html/<str>
	DUP2 html/<str>
	;tag/title html/<close>
	;tag/head html/<close>
	;tag/body html/<open>
	;path/meta-header <build-include>
	;tag/nav html/<open>
	DUP2 #0002 SUB2 LDA chr/hex STHk #00 EQU ?{
		STHkr #02 LTH ?{
			DUP2 lexicon/find-parent lexicon/find-parent <build-children>
			POP2 }
		DUP2 lexicon/find-parent <build-children>
		POP2 }
	POPr <build-children>
	;tag/nav html/<close>
	;ascii/line .File2/write DEO2
	;tag/main html/<open>
	;term/diary #01 html/<photo>
	DUP2 <build-include>
	<build-page-module>
	<build-page-events>
	<build-page-incoming>
	;tag/main html/<close>
	;path/meta-footer <build-include>
	;tag/body html/<close>
	;tag/html html/<close>
	JMP2r

@<build-children> ( name* -- name* )
	;tag/ul html/<open>
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{
			;tag/li html/<open>
			INC2k INC2 html/<local>
			;tag/li html/<close> }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr ;tag/ul html/<close>
	JMP2r

@<build-page-module> ( term* -- term* )
	DUP2 #0001 SUB2 LDA
	( ) DUP [ LIT ", ] NEQ ?{ POP !<build-module-album> }
	diary/count #0002 LTH2 ?{ ;term/name ;err/hidden warning/<print> }
	( ) DUP [ LIT "; ] NEQ ?{ POP !<build-module-portal> }
	( ) DUP [ LIT ". ] NEQ ?{ POP !<build-module-both> }
	( ) [ LIT ": ] NEQ ?{ !<build-module-archive> }
	JMP2r

@<build-page-events> ( name* -- name* )
	;tag/ul html/<open>
	;diary/buf
	&>w
		log/get-rune [ LIT "+ ] NEQ ?{
			;tag/li html/<open>
			DUP2 <build-module-event>
			;tag/li html/<close> }
		str/cap LDAk ?&>w
	POP2 ;tag/ul html/<close>
	JMP2r

@<build-page-incoming> ( -- )
	;incoming/buf LDAk ?{ POP2 JMP2r }
	;tag/p html/<open>
	;dict/incoming-txt ;tag/b html/<auto>
	[ LIT2r =&null ]
	&>w
		DUP2 STH2kr str/cmp ?{
			DUP2 html/<local>
			;ascii/space .File2/write DEO2
			POP2r STH2k }
		str/cap LDAk ?&>w
	POP2 POP2r ;tag/p html/<close>
	JMP2r
	&null ffff

@<build-module-album> ( name* -- name* )
	;diary/buf
	&>w
		log/has-no-picture ?{
			DUP2 ;term/pict log/has-picture ?{ DUP2 #01 html/<photo> } }
		str/cap LDAk ?&>w
	POP2 JMP2r

@<build-module-portal> ( -- )
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{
			INC2k INC2 diary/<load>
			diary/find DUP2 #ffff EQU2 ?{ DUP2 #00 html/<photo> }
			POP2 }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr
	( restore diary ) DUP2 !diary/<load>

@<build-module-archive> ( name* -- name* )
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{ INC2 INC2 DUP2 <build-include> }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr JMP2r

@<build-module-both> ( name* -- name* )
	( stash depth ) DUP2k #0002 SUB2 LDA chr/hex STH
	( start after ) str/cap
	&>w
		( stop at sibling ) LDAk chr/hex STHkr EQU ?&end
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{
			INC2k INC2 diary/<load>
			diary/find DUP2 #ffff EQU2 ?{ DUP2 #00 html/<photo> }
			POP2 }
		( when depth+1 ) LDAk chr/hex STHkr INC NEQ ?{ INC2 INC2 DUP2 <build-include> }
		INC2 INC2 str/cap LDAk ?&>w
	&end ( name* . depth -- )
	POP2 POPr JMP2r

@<build-module-event> ( diary* -- )
	( clip bound ) DUP2 #0005 ADD2 STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;tag/code html/<auto>
	( clip restore ) [ LIT "+ ] STH2r STA
	;dict/mdash-entity-txt html/<str>
	( event name ) DUP2 #000a ADD2 str/walk html/<str>
	JMP2r

@<build-include> ( path* -- )
	path-src-htm/set DUP2 .File/name DEO2
	#8000 .File/length DEO2
	[ LITr 00 ] ;workarea/buf
	&>s
		DUP2 .File/read DEO2
		.File/success DEI2
		( ) DUP2 .File2/length DEO2
		( ) #0000 EQU2 ?{
			DUP2 .File2/write DEO2
			INCr !&>s }
	POP2 STHr ?{ DUP2 ;err/source error/<print> }
	POP2
	( restore ) #0001 .File2/length DEO2
	JMP2r

@workarea/load-file ( path* -- to* from* )
	.File/name DEO2
	#0000 ;&buf SUB2 .File/length DEO2
	;&buf .File/read DEO2
	#0000 ;&buf .File/success DEI2 ADD2 STA2
	( output ) ;&buf .File/success DEI2 ADD2 ;&buf JMP2r

@incoming/<load> ( name* -- )
	path-tmp-map/set-path2 .File/name DEO2
	#1000 .File/length DEO2
	;&buf
	( wipe ) DUP2 [ LIT2 &length $2 ] mem/<clr>
	( read ) DUP2 .File/read DEO2
	( swap ) #0a00 str/<swap-char>
	.File/success DEI2 ,&length STR2
	JMP2r

(
@|Diary )

@log/is-unpict ( log* -- t )
	#0006 ADD2 LDA2k [ LIT2 "00 ] EQU2 ?{ POP2 #00 JMP2r }
	INC2 INC2 LDA [ LIT "0 ] EQU JMP2r

%log/is-ignored ( log* -- log* t ) {
	DUP2 #0005 ADD2 LDA [ LIT ". ] EQU }

%log/is-unevent ( log* -- log* t ) {
	DUP2 #0005 ADD2 LDA [ LIT "+ ] NEQ }

@log/parse ( log* -- next* )
	DUP2 DUP2 #000a ADD2 path-tmp-log/set-path .File2/name DEO2
	html/<str>
	html/<lb>
	DUP2 #0900 str/<swap-char>
	DUP2 diaries/<use>
	log/is-unevent ?{ DUP2 <parse-event> }
	log/is-ignored ?{
		DUP2 log/is-unpict ?{ DUP2 journal/<write-log> } }
	str/cap str/cap !str/cap

@<parse-event> ( event* -- )
	( | add to events )
	;path/events .File2/name DEO2
	( year ) LDAk2 ;&last LDA2 EQU2 ?{
		;tag/br html/<open>
		LDA2k ;&last STA2 }
	( | date )
	;tag/code html/<open>
	DUP2 #0005 html/<length>
	;tag/code html/<close>
	html/<ws>
	str/cap
	( | term )
	DUP2 html/<link>
	;dict/spacer html/<str>
	str/cap
	( | name )
	html/<str>
	;tag/br html/<open>
	html/<lb>
	JMP2r

	&last "17 $1

(
@|Journal )

@sitemap/<delete> ( -- )
	;path/sitemap .File2/name DEO2
	[ LIT2 01 -File2/delete ] DEO
	JMP2r

@sitemap/<build> ( -- )
	[ LIT2 01 -File2/append ] DEO
	#0001 .File2/length DEO2
	( | lexicon )
	;tag/ul html/<open>
	lexicon/<parse>
	;path/sitemap .File2/name DEO2
	;lexicon/buf lexicon/<write-children-rec>
	POP2 ;tag/ul html/<close>
	JMP2r

@journal/<delete> ( -- )
	;path/journal .File2/name DEO2
	[ LIT2 01 -File2/delete ] DEO
	JMP2r

@journal/<write-log> ( photo* -- )
	[ LIT2 &len $2 ] INC2k ,&len STR2
	( max 0x20 ) #0020 LTH2 ?{ POP2 JMP2r }
	;path/journal .File2/name DEO2
	#00 !html/<photo>

(
@|Buffers )

@path/make ( name* -- buf* )
	;&buf ,&ptr STR2
	;&buf SWP2 /<push-str>
	;ext/html
	( >> )

@path/<push-str> ( str* -- )
	&>w
		LDAk DUP #1f GTH ?{ POP POP2 JMP2r }
		/<push>
		INC2 !&>w

@path/<push> ( c -- )
	DUP #20 NEQ ?{ POP LIT "_ }
	#00 [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
	STA2
	JMP2r

@path-abs-site/set ( name* -- buf* )
	;&buf ,&ptr STR2
	;&res SWP2 /<push-str>
	;ext/html
	( >> )

@path-abs-site/<push-str> ( str* -- )
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		#00 [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w

	&res [ "https://wiki.xxiivv.com/site/ &buf $30 ]

@path-abs-diary/get ( diary* -- path* )
	#0006 ADD2
	( > x00 ) LDAk ,&bufa STR
	( > 0xx ) INC2 LDA2 ,&bufb STR2
	;&buf JMP2r

	&buf "https://wiki.xxiivv.com/media/diary/ &bufa $1 &bufb $2 ".jpg $1

@path-site/set ( name* -- buf* )
	;&buf ,&ptr STR2
	;&res SWP2 /<push-str>
	;ext/html
	( >> )

@path-site/<push-str> ( str* -- )
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		#00 [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w

	&res [ "site/ &buf $30 ]

@path-src-htm/set ( name* -- buf* )
	;&buf ,&ptr STR2
	;&res SWP2 /<push-str>
	;ext/htm
	( >> )

@path-src-htm/<push-str> ( str* -- )
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		#00 [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w

	&res [ "src/htm/ &buf $30 ]

@path-tmp-log/set-path ( name* -- buf* )
	;&resbuf ,&ptr STR2
	;&res SWP2
	&>w
		LDAk DUP #1f GTH ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		#00 [ LIT2 &ptr =&resbuf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w

	&res [ "tmp/log- &resbuf $30 ]

@path-tmp-map/set ( str* -- buf* str* )
	;&resbuf ,&ptr STR2
	&>w
		LDAk DUP #1f GTH ?{ &end POP JMP2r }
		DUP [ LIT ". ] EQU ?&end
		DUP [ LIT "_ ] NEQ ?{ POP #20 }
		/<push>
		INC2 !&>w

@path-tmp-map/set-path2 ( name* -- buf* )
	;&resbuf ,&ptr STR2
	;&res SWP2
	&>w2
		LDAk DUP ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		/<push>
		INC2 !&>w2

@path-tmp-map/<push> ( c -- )
	#00 [ LIT2 &ptr =&resbuf ] INC2k ,&ptr STR2
	STA2
	JMP2r

	&res [ "tmp/map- &resbuf $30 ]

@substr/set ( name* -- buf* )
	;&buf ,&ptr STR2
	;&buf SWP2
	&>w
		LDAk DUP #1f GTH ?{ POP POP2 JMP2r }
		#00 [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w
	&buf $30

(
@|Stdlib )

@warning/<print> ( name* msg* -- )
	( msg ) str/<print>
	( name ) str/<print>
	#0a18 DEO
	JMP2r

@error/<print> ( name* msg* -> )
	( msg ) str/<print>
	( name ) str/<print>
	#0a18 DEO
	#010f DEO
	BRK

@str/to-dec ( str* -- val* )
	[ LIT2r 0000 ]
	&>w
		LIT2r 000a MUL2r [ LITr 00 ] LDAk #30 SUB STH
		ADD2r INC2 LDAk ?&>w
	POP2 STH2r JMP2r

@str/head ( addr* -- addr* )
	&>wh
		LDAk ?{ JMP2r }
		#0001 SUB2 !&>wh

@str/next ( str* -- next* )
	&>wn
		LDAk #20 LTH ?{ INC2 !&>wn }
	INC2 JMP2r

@str/walk ( str* -- next* )
	&>ww
		INC2 LDAk #1f GTH ?&>ww
	INC2 JMP2r

@str/cap ( str* -- end* )
	&>wc
		LDAk ?{ INC2 JMP2r }
		INC2 !&>wc

@str/tail ( str* -- end* )
	&>w2
		LDAk ?{ JMP2r }
		INC2 !&>w2

@str/cmp ( a* b* -- bool )
	DUP2k /cap SWP2 SUB2 SWP2 !mem/cmp

@str/cmp3 ( a* b* -- f )
	STH2
	LDAkr LDAk STHr NEQ ?{ INC2r INC2 }
	LDA2r LDA2 STH2r EQU2 JMP2r

@str/<swap-char> ( str* a b -- )
	,&b STR
	,&a STR
	&>wsc
		LDAk [ LIT &a $1 ] NEQ ?{
			STH2k [ LIT &b $1 ] STH2r STA }
		INC2 LDAk ?&>wsc
	POP2 JMP2r

@str/<print-ln> ( str* -- )
	/<print>
	#0a18 DEO
	JMP2r

@str/<print> ( str* -- )
	&>wp
		LDAk DUP ?{ POP POP2 JMP2r }
		#18 DEO
		INC2 !&>wp

@dec/<print> ( dec* -- )
	#000a SWP2 [ LITr ff ]
	&>get
		SWP2k DIV2k MUL2 SUB2 STH
		POP OVR2 DIV2 ORAk ?&>get
	POP2 POP2
	&>put
		STHr INCk ?{ POP JMP2r }
		[ LIT "0 ] ADD #18 DEO !&>put

@chr/hex ( c -- <val> )
	( dec ) LIT "0 SUB DUP #09 GTH [ JMP JMP2r ]
	( hex ) #27 SUB DUP #0f GTH [ JMP JMP2r ]
	( err ) POP #ff JMP2r

@mem/cmp ( a* length* b* -- t )
	STH2
	OVR2 ADD2 SWP2
	&>l
		EQU2k ?{
			LDAk LDAkr STHr NEQ ?{ INC2 INC2r !&>l } }
	POP2r EQU2 JMP2r

@mem/<clr> ( dst* len* -- )
	,&length STR2
	,&dst STR2
	;&mmu .System/expansion DEO2
	JMP2r

	&mmu [ 00 &length $2 0000 &dst $2 00 ]

(
@|Assets )

@header-txt [
	3c68 323e 5468 6520 4361 6c65 6e64 6172
	2073 686f 7773 2075 7063 6f6d 696e 6720
	616e 6420 7061 7374 2065 7665 6e74 7320
	6672 6f6d 2074 6865 206a 6f75 726e 616c
	2e3c 2f68 323e 0a0a 3c70 3e54 6869 7320
	7769 6b69 2075 7365 7320 7468 6520 3c61
	2068 7265 663d 2761 7276 656c 6965 2e68
	746d 6c27 3e41 7276 656c 6965 3c2f 613e
	2074 696d 6520 666f 726d 6174 2c20 7768
	6572 6520 7468 6520 7965 6172 2069 7320
	6469 7669 6465 6420 696e 2032 3620 7065
	7269 6f64 732c 206f 7220 3c69 3e6d 6f6e
	7468 733c 2f69 3e2c 206f 6620 3134 2064
	6179 732c 206e 756d 6265 7265 6420 6672
	6f6d 2041 2074 6f20 5a2e 2054 6865 2069
	6e69 7469 616c 206c 6f67 6769 6e67 2079
	6561 7220 616e 6420 7468 6520 4172 7665
	6c69 6520 6461 7465 7320 636f 756e 7420
	7570 7761 7264 2066 726f 6d20 3230 3036
	2e20 596f 7520 6361 6e20 7365 6520 6d6f
	7265 2075 7064 6174 6573 2069 6e20 7468
	6520 3c61 2068 7265 663d 276a 6f75 726e
	616c 2e68 746d 6c27 3e6a 6f75 726e 616c
	3c2f 613e 2061 6e64 203c 6120 6872 6566
	3d27 6e6f 772e 6874 6d6c 273e 6e6f 773c
	2f61 3e20 7061 6765 733c 2f70 3e0a 0a00 ]

@ascii/space 20 &line 0a
	&lt "<
	&gt ">
	&equal "=
	&quote ""
	&slash "/

@tag/html "html $1
	&head "head $1
	&meta "meta $1
	&title "title $1
	&body "body $1
	&nav "nav $1
	&main "main $1
	&br "br $1
	&p "p $1
	&b "b $1
	&ul "ul $1
	&li "li $1
	&a "a $1
	&span "span $1
	&code "code $1
	&figure "figure $1
	&figcaption "figcaption $1

@attr/class "class $1
	&property "property $1
	&content "content $1

@og/title "og:title $1
	&image "og:image $1
	&url "og:url $1

@ext/html ".html $1
	&htm ".htm $1

@err/source "-- 20 "Source: 20 $1
	&orphan "-- 20 "Orphan: 20 $1
	&hidden "-- 20 "Hidden: 20 $1

@path/lexicon "src/tables/lexicon $1
	&sitemap "src/htm/sitemap.htm $1
	&events "src/htm/events.htm $1
	&journal "src/htm/journal.htm $1
	&default-image "https://wiki.xxiivv.com/media/ "services/rss.jpg $1
	&meta-head "meta_head $1
	&meta-header "meta_header $1
	&meta-footer "meta_footer $1

@diary-paths
	&3 "src/tables/diary/15-19.tsv $1
	&2 "src/tables/diary/10-14.tsv $1
	&1 "src/tables/diary/05-09.tsv $1
	&0 "src/tables/diary/00-04.tsv $1

@diary-path-pages [
	=diary-paths/3 =diary-paths/2
	=diary-paths/1 =diary-paths/0 ] &end

@dict/xxiivv-txt "XXIIVV $1
	&self-txt "self $1 &mdash-entity-txt 20 "&mdash; 20 $1
	&parent-txt "parent $1
	&doctype "<!DOCTYPE 20 "html> $1
	&no-picture "000 $1
	&incoming-txt "incoming: 20 $1
	&right-txt "right $1 &spacer 20 "&mdash; 20 $1
	&available-txt ".. 20 "Available 20 "diary: 20 $1
	&warn-duplicate "-- 20 "Duplicate 20 "diary: 20 $1
	&warn-unknown "-- 20 "Unknown 20 "diary: 20 $1 &in 20 "in 20 $1
	&err-overflow "!! 20 "Overflow: 20 $1
	&err-redlink "!! 20 "Redlink: 20 $1

(
@|memory )

@diaries/buf $100

@lexicon/buf $2000

@diary/buf $1000

@incoming/buf $1000

@workarea/buf

