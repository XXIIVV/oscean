( Oh time thy pyramids )

|a0 @File/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|100

@on-reset ( -> )
	( | clear old copies )
	;dict/journal-path .File2/name DEO2
	[ LIT2 01 -File2/delete ] DEO
	( | setup output )
	[ LIT2 01 -File2/append ] DEO
	#0001 .File2/length DEO2
	( | lexicon )
	;dict/sitemap-path .File2/name DEO2
	[ LIT2 01 -File2/delete ] DEO
	;dict/ul-tag <w>
	lexicon/<parse>
	;dict/ul-tag </w>
	( | diary )
	;dict/events-path .File2/name DEO2
	[ LIT2 01 -File2/delete ] DEO
	;header-txt <str>
	;dict/p-tag <w>
	<parse-diary-pages>
	;dict/p-tag </w>
	( | print next available id )
	;dict/available-txt str/<print>
	find-available dec/<print>
	#0a18 DEO
	( | halt. )
	#800f DEO
	BRK

(
@|lexicon )

%<lb> { ;dict/0a .File2/write DEO2 }

%<ws> { ;dict/20 .File2/write DEO2 }

@<load-file> ( addr* length* path* -- )
	.File/name DEO2
	DUP2 .File/length DEO2
	SWP2 .File/read DEO2
	( check for size ) .File/success DEI2 NEQ2 ?{ .File/name DEI2 !str/<print-ln> }
	JMP2r

@lexicon/find ( name* -- term* )
	,&t STR2
	;&buf
	&>w
		INC2 INC2 DUP2 [ LIT2 &t $2 ] scmp ?{
			str/cap LDAk ?&>w
		POP2 #ffff }
	JMP2r

@lexicon/<parse> ( -- )
	;&buf
	( load ) DUP2 #2000 ;dict/lexicon-path <load-file>
	( swap ) DUP2 #0a00 str/<swap-char>
	( range ) .File/success DEI2 ADD2k NIP2 SWP2
	&>l
		INC2 INC2 DUP2 DUP2 ,&src STR2
		htm-path/set workarea/get-length ROT2 <load-file>
		;workarea/buf .File/success DEI2 ADD2k NIP2 SWP2
		&>ll
			LDA2k [ LIT2 "hr ] NEQ2 ?{
				INC2k INC2 LDA2 [ LIT2 "ef ] NEQ2 ?{
					[ LIT2 &src $2 ] OVR2 #0006 ADD2 <parse-link> } }
			INC2 GTH2k ?&>ll
		POP2 POP2
		( next ) str/cap GTH2k ?&>l
	POP2 POP2
	( | sitemap )
	;dict/sitemap-path .File2/name DEO2
	;lexicon/buf <write-children-rec>
	POP2 JMP2r

@<write-children-rec> ( addr* -- next* )
	( | take depth )
	LDAk STH
	( | name )
	;dict/li-tag <w>
	INC2 INC2 DUP2 <link>
	;dict/li-tag </w>
	<lb>
	str/cap
	( | children )
	LDAk STHkr INC NEQ ?{
		;dict/ul-tag <w>
		<write-children-rec>
		;dict/ul-tag </w> }
	( | sibling )
	LDAk STHkr EQU ?{ POPr JMP2r }
	POPr !<write-children-rec>

@<parse-link> ( src* link* -- )
	( external ) ;dict/http-txt OVR2 sseg ?&end
	( hash ) LDAk [ LIT "# ] EQU ?&end
	( rel ) LDAk [ LIT ". ] EQU ?&end
	( | find broken links )
	DUP2 ;&buf <scpy-line>
	;&buf [ LIT2 "_ 20 ] str/<swap-char>
	;&buf lexicon/find INC2 ORA ?{
		;dict/redlink str/<print>
		;&buf str/<print>
		;dict/in str/<print>
		OVR2 str/<print-ln>
		#010f DEO }
	DUP2 ;&term <scpy-line>
	;&term [ LIT2 20 "_ ] str/<swap-char>
	( res ) ;&map-path .File2/name DEO2
	OVR2 <str>
	<lb>
	&end ( src* link* -- )
	POP2 POP2 JMP2r
	&buf $30
	&map-path [ "tmp/map- &term $30 ]

(
@|diary )

@<parse-diary-pages> ( -- )
	#4000 .File/length DEO2
	;diary-path-pages/end ;diary-path-pages
	&>l
		LDA2k workarea/get-length ROT2 <load-file>
		#0000
		( to* from* ) ;workarea/buf .File/success DEI2 ADD2
		( cap ) STA2k NIP2 ;workarea/buf <parse-diary-page>
		INC2 INC2 GTH2k ?&>l
	POP2 POP2 JMP2r

@<parse-diary-page> ( to* from* -- )
	DUP2 #0a00 str/<swap-char>
	&>l
		<parse-diary-line>
		INC2 GTH2k ?&>l
	POP2 POP2 JMP2r

@<parse-diary-line> ( line* -- next* )
	DUP2k #000a ADD2 ;&term <scpy-line>
	( | does it exists? )
	;&term lexicon/find INC2 ORA ?{
		;dict/warn-unknown str/<print>
		DUP2 str/<print>
		#0a18 DEO }
	;&term [ LIT2 20 "_ ] str/<swap-char>
	( res ) ;&log-path .File2/name DEO2
	<str>
	<lb>
	( split ) DUP2 #0900 str/<swap-char>
	DUP2 set-diary
	( event ) DUP2 #0005 ADD2 LDA [ LIT "+ ] NEQ ?{ DUP2 <parse-event> }
	( photo ) DUP2 #0005 ADD2 LDA [ LIT ". ] EQU ?{
		;dict/no-photo OVR2 #0006 ADD2 scmp ?{ DUP2 <add-to-journal> } }
	( eol ) str/cap str/cap !scap/

	&log-path [ "tmp/log- &term $30 ]

@<parse-event> ( event* -- )
	( | add to events )
	;dict/events-path .File2/name DEO2
	( year ) LDAk2 ,&last LDR2 EQU2 ?{
		;dict/br-tag <w>
		LDA2k ,&last STR2 }
	( | date )
	;dict/code-tag <w>
	DUP2 #0005 <len>
	;dict/code-tag </w>
	<ws>
	str/cap
	( | term )
	DUP2 <link>
	;dict/spacer <str>
	str/cap
	( | name )
	<str>
	;dict/br-tag <w>
	<lb>
	JMP2r

	&last "17 $1

@<add-to-journal> ( photo* -- )
	( | limit to 20 entries )
	[ LIT2 &len $2 ] #0020 LTH2 ?{ POP2 JMP2r }
	;dict/journal-path .File2/name DEO2
	DUP2 #0006 ADD2 ,&pict STR2
	#000a ADD2 DUP2 ,&term STR2
	str/cap ,&title STR2
	;dict/figure-tag <w>
	( src ) [ LIT2 &pict $2 ]
	( alt ) [ LIT2 &title $2 ] <img>
	;dict/figcaption-tag <w>
	[ LIT2 &term $2 ] <link>
	;dict/spacer <str>
	,&title LDR2 <str>
	;dict/figcaption-tag </w>
	;dict/figure-tag </w>
	,&len LDR2 INC2 ,&len STR2
	<lb>
	JMP2r

@workarea/get-length ( -- buf* length* )
	;&buf #0000 OVR2 SUB2 JMP2r

(
@|validate )

@set-diary ( log* -- )
	( | validate photo id )
	#0006 ADD2 str/to-dec ORAk ?{ POP2 JMP2r }
	DUP2 get-diary ?{
		( bit ) #01 OVR #07 AND #40 SFT SFT STH
		( res ) #03 SFT2 ;diaries/buf ADD2 LDAk STHr ORA ROT ROT STA
		JMP2r }
	;dict/warn-duplicate str/<print>
	dec/<print>
	#0a18 DEO
	JMP2r

@get-diary ( id* -- mask )
	( bit ) #01 OVR #07 AND #40 SFT SFT STH
	( res ) #03 SFT2 ;diaries/buf ADD2 LDA STHr AND JMP2r

@find-available ( -- id* )
	#0001
	&>w
		DUP2 get-diary ?{ JMP2r }
		INC2 !&>w

(
@|framework )

@<w> ( name* -- )
	;&a .File2/write DEO2
	<str>
	;&b .File2/write DEO2
	JMP2r

	&a "<
	&b ">

@</w> ( name* -- )
	;&a .File2/write DEO2
	;&b .File2/write DEO2
	<str>
	;&c .File2/write DEO2
	JMP2r

	&a "<
	&b "/
	&c ">

@<img> ( src* alt* -- )
	;&a <str>
	<str>
	;&b <str>
	<str>
	;&c !<str>

	&a "<img 20 "alt=" $1
	&b "" 20 "src="../media/diary/ $1
	&c ".jpg"> $1

@<link> ( str* -- )
	;&a <str>
	DUP2 <path>
	;&b <str>
	<str>
	;&c !<str>

	&a "<a 20 "href=' $1
	&b ".html'> $1
	&c "</a> $1

@<path> ( str* -- )
	LDAk [ LIT "_ ] OVR #20 NEQ [ JMP SWP POP ] ,&a STR
	;&a .File2/write DEO2
	INC2 LDAk ?<path>
	POP2 JMP2r
	&a $1

@<str> ( str* -- )
	DUP2 scap/ OVR2 SUB2
	( >> )

@<len> ( str* len* -- )
	.File2/length DEO2
	.File2/write DEO2
	( restore ) #0001 .File2/length DEO2
	JMP2r

(
@|buffers )

@htm-path/set ( name* -- res* )
	;&buf ,&ptr STR2
	;&res SWP2 /<push-str>
	;dict/htm-ext
	( >> )

@htm-path/<push-str> ( str* -- )
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		DUP #20 NEQ ?{ POP LIT "_ }
		#00 [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
		STA2
		INC2 !&>w

	&res [ "src/htm/ &buf $30 ]

(
@|stdlib )

@sseg ( a* b* -- f )
	STH2
	&>l
		LDAk LDAkr STHr NEQ ?{
			INC2k LDA #00 EQU ?{ INC2 INC2r !&>l } }
	LDA LDAr STHr EQU JMP2r

@<scpy-line> ( src* dst* -- )
	STH2
	&>w
		LDAk STH2kr STA
		INC2r INC2 LDAk DUP #1f GTH SWP LIT ". NEQ AND ?&>w
	POP2 #00 STH2r STA
	JMP2r

@scmp ( a* b* -- f )
	STH2
	&>l
		LDAk ?{ &d LDA LDAr STHr EQU JMP2r }
		LDAk LDAkr STHr NEQ ?&d
		INC2 INC2r !&>l

@scap ( str* -- end* )
	INC2 & LDAk ?scap
	JMP2r

@str/cap ( str* -- end* )
	&>wc
		LDAk ?{ INC2 JMP2r }
		INC2 !&>wc

@str/<swap-char> ( str* a b -- )
	,&b STR
	,&a STR
	&>wsc
		LDAk [ LIT &a $1 ] NEQ ?{
			STH2k [ LIT &b $1 ] STH2r STA }
		INC2 LDAk ?&>wsc
	POP2 JMP2r

@str/to-dec ( str* -- val* )
	[ LIT2r 0000 ]
	&>w
		LIT2r 000a MUL2r LITr 00 LDAk #30 SUB STH
		ADD2r INC2 LDAk ?&>w
	POP2 STH2r JMP2r

@str/<print> ( str* -- )
	LDAk DUP ?{ POP POP2 JMP2r }
	#18 DEO
	INC2 !/<print>

@str/<print-ln> ( str* -- )
	/<print>
	#0a18 DEO
	JMP2r

@dec/<print> ( dec* -- )
	#000a SWP2 [ LITr ff ]
	&>get
		SWP2k DIV2k MUL2 SUB2 STH
		POP OVR2 DIV2 ORAk ?&>get
	POP2 POP2
	&>put
		STHr INCk ?{ POP JMP2r }
		[ LIT "0 ] ADD #18 DEO !&>put

(
@|assets )

@dict
	&no-photo "000 $1 &spacer 20 "&mdash; 20 $1
	&journal-path "src/htm/journal.htm $1
	&http-txt "http $1
	&htm-ext ".htm $1
	&p-tag "p $1
	&ul-tag "ul $1
	&li-tag "li $1
	&br-tag "br $1
	&code-tag "code $1
	&figure-tag "figure $1
	&figcaption-tag "figcaption $1
	&lexicon-path "src/tables/lexicon $1
	&sitemap-path "src/htm/sitemap.htm $1
	&events-path "src/htm/events.htm $1
	&redlink "!! 20 "Redlink: 20 $1
	&available-txt ".. 20 "Available 20 "diary: 20 $1
	&warn-duplicate "-- 20 "Duplicate 20 "diary: 20 $1
	&warn-unknown "-- 20 "Unknown 20 "diary: 20 $1 &in 20 "in 20 $1 &0a 0a &20 20

@diary-paths
	&3 "src/tables/diary/15-19.tsv $1
	&2 "src/tables/diary/10-14.tsv $1
	&1 "src/tables/diary/05-09.tsv $1
	&0 "src/tables/diary/00-04.tsv $1

@diary-path-pages [
	=diary-paths/3 =diary-paths/2
	=diary-paths/1 =diary-paths/0 ] &end

@header-txt [
	3c68 323e 5468 6520 4361 6c65 6e64 6172
	2073 686f 7773 2075 7063 6f6d 696e 6720
	616e 6420 7061 7374 2065 7665 6e74 7320
	6672 6f6d 2074 6865 206a 6f75 726e 616c
	2e3c 2f68 323e 0a0a 3c70 3e54 6869 7320
	7769 6b69 2075 7365 7320 7468 6520 3c61
	2068 7265 663d 2761 7276 656c 6965 2e68
	746d 6c27 3e41 7276 656c 6965 3c2f 613e
	2074 696d 6520 666f 726d 6174 2c20 7768
	6572 6520 7468 6520 7965 6172 2069 7320
	6469 7669 6465 6420 696e 2032 3620 7065
	7269 6f64 732c 206f 7220 3c69 3e6d 6f6e
	7468 733c 2f69 3e2c 206f 6620 3134 2064
	6179 732c 206e 756d 6265 7265 6420 6672
	6f6d 2041 2074 6f20 5a2e 2054 6865 2069
	6e69 7469 616c 206c 6f67 6769 6e67 2079
	6561 7220 616e 6420 7468 6520 4172 7665
	6c69 6520 6461 7465 7320 636f 756e 7420
	7570 7761 7264 2066 726f 6d20 3230 3036
	2e20 596f 7520 6361 6e20 7365 6520 6d6f
	7265 2075 7064 6174 6573 2069 6e20 7468
	6520 3c61 2068 7265 663d 276a 6f75 726e
	616c 2e68 746d 6c27 3e6a 6f75 726e 616c
	3c2f 613e 2061 6e64 203c 6120 6872 6566
	3d27 6e6f 772e 6874 6d6c 273e 6e6f 773c
	2f61 3e20 7061 6765 733c 2f70 3e0a 0a00 ]

(
@|memory )

@lexicon/buf $2000

@diaries/buf $100

@workarea/buf

