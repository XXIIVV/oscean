<!DOCTYPE html><html><head>
<link href="../links/main.css" type="text/css" rel="stylesheet">
<link href="../media/services/icon.png" type="image/png" rel="shortcut icon">
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta property='og:type' content='website'/>
<meta property='og:site_name' content='XXIIVV'/>
<meta property='og:description' content="By Devine Lu Linvega"/>
<meta property='og:logo' content="https://wiki.xxiivv.com/media/services/rss.jpg"/>
<meta property="og:title" content="uxntal library"/><meta property="og:url" content="https://wiki.xxiivv.com/site/uxntal_library.html"/><meta property="og:image" content="https://wiki.xxiivv.com/media/services/rss.jpg"/><title>XXIIVV &mdash; uxntal library</title></head><body>
<header><a href="home.html"><img alt="XXIIVV" src="../media/icon/logo.svg"></a></header>
<nav><ul><li><a href="uxntal_syntax.html">uxntal syntax</a></li><li><a href="uxntal_stacks.html">uxntal stacks</a></li><li><a href="uxntal_opcodes.html">uxntal opcodes</a></li><li><a href="uxntal_modes.html">uxntal modes</a></li><li><a href="uxntal_immediate.html">uxntal immediate</a></li><li><a href="uxntal_memory.html">uxntal memory</a></li><li><a href="uxntal_devices.html">uxntal devices</a></li><li><a href="uxntal_software.html" class="parent">uxntal software</a></li></ul><ul><li><a href="uxntal_library.html" class="self">uxntal library</a></li><li><a href="uxnfor.html">uxnfor</a></li><li><a href="uxnlin.html">uxnlin</a></li><li><a href="drifblim.html">drifblim</a></li><li><a href="bicycle.html">bicycle</a></li><li><a href="beetbug.html">beetbug</a></li></ul><ul><li><a href="uxntal_signed.html">uxntal signed</a></li><li><a href="uxntal_labyrinth.html">uxntal labyrinth</a></li></ul></nav>
<main><h2>A collection of commonly used routines in Uxntal projects.</h2>

<p>The following snippets are in the standard format. If you discover faster and smaller helpers, please get in touch with me.</p>

<h3>Hexadecimal Numbers</h3>

<p>To print an hexadecimal number:</p>
<pre>
@&lt;phex&gt; ( short* -: )
	SWP /b
	&b ( byte -: )
		DUP #04 SFT /c
	&c ( byte -: )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
		JMP2r
</pre>

<p>To convert an hexadecimal string to a value:</p>
<pre>
@shex ( str* -: val* )
	[ LIT2r 0000 ]
	&w ( str* `acc* -: val* )
	LDAk chex INC #00 EQU ?{
		[ LITr 40 ] SFT2r LDAk chex [ LITr 00 ] STH
		ADD2r INC2 LDAk ?&w }
	POP2 STH2r JMP2r
</pre>

<p>To convert an hexadecimal character to a nibble:</p>
<pre>
@chex ( c -: val! )
	( dec ) [ LIT "0 ] SUB DUP #09 GTH ?{ JMP2r }
	( hex ) #27 SUB DUP #0f GTH ?{ JMP2r }
	( err ) POP #ff JMP2r
</pre>

<h3>Decimal Numbers</h3>

<p>To print a decimal short to decimal:</p>
<pre>
@pdec ( short* -- )
	#000a SWP2 [ LITr ff ]
	&>get ( -- )
		SWP2k DIV2k MUL2 SUB2 STH
		POP OVR2 DIV2 ORAk ?&>get
	POP2 POP2
	&>put ( -- )
		STHr INCk ?{ POP JMP2r }
		[ LIT "0 ] ADD #18 DEO !&>put
</pre>

<p>To print a decimal byte to decimal:</p>

<pre>
@print-dec ( dec -- )
	DUP #64 DIV print-num/try
	DUP #0a DIV print-num/try
	( >> )

@print-num ( num -- )
	#0a DIVk MUL SUB [ LIT "0 ] ADD #18 DEO
	JMP2r
	&try ( num -- )
		DUP ?print-num
		POP JMP2r</pre>

<p>To convert a decimal string to a hexadecimal value.</p>
<pre>
@sdec ( str* -- val* )
	[ LIT2r 0000 ]
	&w ( -- )
		( validate ) LDAk [ LIT "0 ] SUB #09 GTH ?&end
		( accumulate ) [ LIT2r 000a ] MUL2r
		( combine ) LDAk [ LIT "0 ] SUB [ LITr 00 ] STH ADD2r
		( continue ) INC2 LDAk ?&w
	&end POP2 STH2r JMP2r
</pre>

<h3 id='strings'>Strings</h3>

<p>To print a string.</p>
<pre>
@&lt;pstr&gt; ( str* -: )
	LDAk #18 DEO
	INC2 & LDAk ?&lt;pstr&gt;
	POP2 JMP2r
</pre>

<p>Helpers for strings:</p>

<pre>
[TODO]
</pre>

<h3>Memory</h3>

<p>To print an entire page of memory:</p>
<pre>
@pmem ( addr* -- )
	#0000
	&l ( -- )
		ADD2k LDA phex/b
		DUP #0f AND #0f NEQ #16 MUL #0a ADD #18 DEO
		INC NEQk ?&l
	POP2 POP2 JMP2r
</pre>

<p>Helpers for memory.</p>

<pre>
[TODO]
</pre>

<p>Helpers for bitwise operations.</p>

<pre>
@popcount ( byte -- count ) LITr 00 #00 &w SFTk #01 AND STH ADDr INC SFTk ?&w POP2 STHr JMP2r
@popcnt ( v* -- num ) LITr 00 &>w #01 ANDk STH ADDr SFT2 ORAk ?&>w POP2 STHr JMP2r
</pre>

<h3>Dates</h3>

<p>To find the day of the week from a given date, <a href='https://c-faq.com/misc/zeller.html' target='_blank'>Tomohiko Sakamoto</a>'s method:</p>
<pre>
@dotw ( y* m d -- dotw )
	( y -= m < 3; )
	OVR STH SWP2 #00 STHr #02 LTH SUB2
	STH2
	( t[m-1] + d )
	#00 ROT ;&t ADD2 LDA #00 SWP
	ROT #00 SWP ADD2
	( y + y/4 - y/100 + y/400 )
	STH2kr
	STH2kr #02 SFT2 ADD2
	STH2kr #0064 DIV2 SUB2
	STH2r #0190 DIV2 ADD2
	ADD2
	( % 7 )
	#0007 DIV2k MUL2 SUB2 NIP
	JMP2r
		&t [ 00 03 02 05 00 03 05 01 04 06 02 04 ]
</pre>

<p>To find if a year is a leap year:</p>
<pre>
@is-leap-year ( year* -- bool )
	( leap year if perfectly divisible by 400 )
	DUP2 #0190 ( MOD2 ) DIV2k MUL2 SUB2 #0000 EQU2 ?&leap
	( not a leap year if divisible by 100 )
	( but not divisible by 400 )
	DUP2 #0064 ( MOD2 ) DIV2k MUL2 SUB2 #0000 EQU2 ?&not-leap
	( leap year if not divisible by 100 )
	( but divisible by 4 )
	DUP2 #0003 AND2 #0000 EQU2 ?&leap
	( all other years are not leap years )
	&not-leap
	POP2 #00
	JMP2r
		&leap POP2 #01 JMP2r
</pre>

<h3>Memory</h3>

<pre>
@msfl ( b* a* len* -- )
	STH2
	SWP2 EQU2k ?&end
	&l ( -- )
		DUP2k STH2kr ADD2 LDA ROT ROT STA
		INC2 GTH2k ?&l
	POP2 POP2 &end POP2r JMP2r

@msfr ( b* a* len* -- )
	STH2
	EQU2k ?&end
	&l ( -- )
		DUP2 LDAk ROT ROT STH2kr ADD2 STA
		#0001 SUB2 LTH2k ?&l
	POP2 POP2 &end POP2r JMP2r
</pre>

<h3>Random</h3>

<pre>
@prng-init ( -- )
	[ LIT2 00 -DateTime/second ] DEI
		[ LIT2 00 -DateTime/minute ] DEI #60 SFT2 EOR2
		[ LIT2 00 -DateTime/hour ] DEI #c0 SFT2 EOR2 ,prng/x STR2
	[ LIT2 00 -DateTime/hour ] DEI #04 SFT2
		[ LIT2 00 -DateTime/day ] DEI #10 SFT2 EOR2
		[ LIT2 00 -DateTime/month ] DEI #60 SFT2 EOR2
		.DateTime/year DEI2 #a0 SFT2 EOR2 ,prng/y STR2
	JMP2r

@prng ( -- number* )
	[ LIT2 &x $2 ]
		DUP2 #50 SFT2 EOR2
		DUP2 #03 SFT2 EOR2
	[ LIT2 &y $2 ] DUP2 ,&x STR2
		DUP2 #01 SFT2 EOR2 EOR2
		,&y STR2k POP
	JMP2r
</pre>

<h3>Misc</h3>

<p>To convert a signed byte to a signed short.</p>

<pre>DUP #7f GTH #ff MUL SWP</pre>

<pre>
@smax ( x* y* -> smax* ) EOR2k POP #80 AND ?min !max
@min ( x* y* -> min* ) LTH2k JMP SWP2 POP2 JMP2r
@max ( x* y* -> max* ) LTH2k JMP SWP2 NIP2 JMP2r
@mod ( x y -- z ) DIVk MUL SUB JMP2r
@mod2 ( x* y* -- z* ) DIV2k MUL2 SUB2 JMP2r

( Signed macros )

@abs ( a -- b ) DUP #80 LTH ?{ #00 SWP SUB } JMP2r
@abs2 ( a* -- b* ) DUP2k #1f SFT2 MUL2 SUB2 JMP2r
@lts2 ( a* b* -- f ) #8000 STH2k ADD2 SWP2 STH2r ADD2 GTH2 JMP2r
@gts2 ( a* b* -- f ) #8000 STH2k ADD2 SWP2 STH2r ADD2 LTH2 JMP2r

( Binary macros )

@rol ( x y -- z ) DUP #07 SFT SWP #10 SFT ADD JMP2r
@ror ( x y -- z ) DUP #70 SFT SWP #01 SFT ADD JMP2r
@rol2 ( x* y* -- z* ) DUP2 #0f SFT2 SWP2 #10 SFT2 ADD2 JMP2r
@ror2 ( x* y* -- z* ) DUP2 #f0 SFT2 SWP2 #01 SFT2 ADD2 JMP2r
</pre>

<h2>Using and operating on negative numbers in Uxntal.</h2>

<p>Uxn doesn't have built-in support for negative integers. However, you can
emulate <a href='https://en.wikipedia.org/wiki/Signed_number_representations'
target='_blank'>signed numbers</a> by treating some unsigned values as
negative. For example, treating unsigned bytes as signed results in the
following:</p>

<table border='1' style='font-family:monospace'>
	<tr>
	<th>hex</th>
	<td>00</td><td>01</td><td>02</td>
	<td rowspan='3'></td>
	<td>7e</td><td>7f</td><td><b>80</b></td><td>81</td><td>82</td>
	<td rowspan='3'></td>
	<td>fd</td><td>fe</td><td>ff</td>
	</tr>
	<tr>
	<th>unsigned</th>
	<td>0</td><td>1</td><td>2</td>
	<td>126</td><td>127</td><td>128</td><td>129</td><td>130</td>
	<td>253</td><td>254</td><td>255</td>
	</tr>
	<tr>
	<th>signed</th>
	<td>0</td><td>1</td><td>2</td>
	<td>126</td><td>127</td><td>-128</td><td>-127</td><td>-126</td>
	<td>-3</td><td>-2</td><td>-1</td>
	</tr>
</table>

<p>The first 128 integers (0-127) are represented the same as unsigned and
signed, but the latter 128 are different. The basic idea here is that for
values greater than #7f (127) we subtract 256 to get their <i>signed value</i>:</p>

<pre>signed = n < 128 ? n : n - 256</pre>

<p>It turns out that many unsigned operations "work" even when treating the values
as signed. (In other words, you get the same result as you would have using a
language with signed integer types.) The following arithmetic instructions work
correctly with "signed" values:</p>

<pre>
#13 #ff ADD returns #12
#02 #03 SUB returns #ff
#02 #ff MUL returns #fe
</pre>

<p>Be careful! The smallest negative value (-128 for bytes, -32768 for shorts)
has no corresponding positive value. This means that some operations will
not work as expected:</p>

<pre>
#80 #ff MUL returns #80 (-128 * -1 = -128)
#00 #80 SUB returns #80 (0 - (-128) = -128)
</pre>

<p>Also, negative and positive values will "wrap around" in the usual way when
dealing with two's-complement representations:</p>

<pre>
#7f #01 ADD returns #80 (127 + 1 = -128)
#80 #01 SUB returns #7f (-128 - 1 = 127)
#80 #80 ADD returns #00 (-128 + (-128) = 0)
</pre>

<p>Other instructions will not handle "negative" integers correctly. These
routines will safely compare "signed" bytes:</p>

<pre>
@signed-lth ( x y -- res )
	DUP2 #8080 AND2 EQU ?&diff LTH JMP2r &diff LTH #00 NEQ JMP2r

@signed-gth ( x y -- res )
	DUP2 #8080 AND2 EQU ?&diff GTH JMP2r &diff GTH #00 NEQ JMP2r
</pre>

<p>Similarly, division will not correctly handle signed values. The simplest
way to handle this is to make both values non-negative, do unsigned
division (i.e. DIV) and then set the correct sign at the end.</p>

<pre>
@abs ( x -- abs-x sign )
	DUP #7f GTH #fe MUL INC STHk MUL STHr JMP2r

@signed-div ( x y -- x/y )
	abs STH SWP abs STH SWP DIV MULr STHr MUL JMP2r
</pre>

<p>The unsigned shift operator treats the sign bit like any other. This means
shifting left will lose the sign bit (reversing the sign) and that shifting
right will convert the sign bit into a value bit. Signed numbers will also need
their own routines for decimal input and output, if those are required by your
program.</p>

<pre>
@signed-print ( num -- )
	( - ) DUP #80 LTH ?{ LIT "- #18 DEO #7f AND #80 SWP SUB }
	( 100 ) DUP #64 DIV signed-print/emit
	( 10 ) DUP #0a DIV signed-print/base
	&base ( digit -- ) #0a DIVk MUL SUB
	&emit ( digit -- ) LIT "0 ADD #18 DEO JMP2r
</pre>

<p>If you need a sign-aware shift you'll likely want to convert negatives to
positive values, perform a shift, and then restore the sign. Keep in mind
that -128 cannot be converted to a positive value, and may require special
treatment.</p>

<ul>
	<li>Guide by <a href='http://plastic-idolatry.com/erik/nxu/math-notes.txt' target='_blank'>d_m</a></li>
</ul>

<h2>I have reordered time. I have turned the world upside down, and I have done
it all for you!</h2>

<p>This <b>self-replicating program</b> will emit its own bytecode when run:</p>

<!-- a001 0fa0 0100 9480 1817 21aa 20ff f7 -->

<div class="codeview">
<pre class='src'>
@q ( -> )
	;&end ;q &l LDAk #18 DEO INC2 GTH2k ?&l &end
</pre>
<pre class='cmd'>uxnasm src.tal seed.rom && uxncli seed.rom > res.rom</pre>
</div>

<p>This <b>cyclical self-replicating program</b> will emit a program that prints <i>ying</i> and which emits a program like itself that will print <i>yang</i>, which in turn will emit a program that prints <i>ying</i> again, and so forth: </p>

<div class="codeview">
<pre class='src'>
@y ( -> )
	[ LIT2 "y 19 ] DEO [ LIT2 &c "ai ] SWPk ,&c STR2 #19 DEO 
	[ LIT2 "n 19 ] DEO [ LIT2 "g 19 ] DEO 
	;&end ;y &w LDAk #18 DEO INC2 GTH2k ?&w &end
</pre>
<pre class='cmd'>uxnasm yingyang.tal ying.rom && uxncli ying.rom > yang.rom</pre>
</div>

<p>This <b>quine program</b> will emit a second program that emits its own bytecode as
hexadecimal ascii characters, which is also a valid program source:</p>

<!--
@q ( -> )
	;end ;q
	&l LDA2k phex #2018 DEO INC2 INC2 GTH2k ?&l
	BRK
@phex ( short* -- )
	SWP phex/b
	&b DUP #04 SFT phex/c
	&c #0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
	JMP2r
@end
-->

<div class="codeview">
<pre class='src'>
a001 32a0 0100 b460 000b a020 1817 2121 
aa20 fff2 0004 6000 0006 8004 1f60 0000 
800f 1c06 8009 0a80 271a 1880 3018 8018 
176c
</pre>
<pre class='cmd'>uxnasm src.tal seed.rom && uxncli seed.rom > src.tal</pre>
</div>

<p>This <b>ambigram program</b> executes the same backward or forward, every single opcode is evaluated, and prints the palindrome "tenet". It is my entry to  <a href='https://binary.golf/1/' target='_blank'>BGGP1</a>:</p>

<!--
DEO POP [ LIT2 18 LIT2 ] POP [ LIT2 "t LIT2 ] POP SWP DEO
DEO POP [ LIT2 18 LIT2 ] POP [ LIT2 "e LIT2 ] POP SWP DEO
DEO POP [ LIT2 18 LIT2 ] POP [ LIT2 "n LIT2 ] POP [ LIT2 18 LIT2 ] POP DEO
DEO SWP POP [ LIT2 "e LIT2 ] POP [ LIT2 18 LIT2 ] POP DEO
DEO SWP POP [ LIT2 "t LIT2 ] POP [ LIT2 18 LIT2 ] POP DEO
-->

<div class="codeview">
<pre class='src'>
1702 a018 a002 a074 a002 0417 1702 a018
a002 a065 a002 0417 1702 a018 a002 a06e
a002 a018 a002 1717 0402 a065 a002 a018
a002 1717 0402 a074 a002 a018 a002 17
</pre>
<pre class='cmd'>uxnasm src.tal turnstile.rom && uxncli turnstile.rom</pre>
</div>

<p>This <b>self-replicating program</b> produces exactly 1 copy of itself, names the copy "4", does not execute the copied file, and prints the number 4. It is my 19 bytes entry to  <a href='https://binary.golf/4/' target='_blank'>BGGP4</a>:</p>

<div class="codeview">
<pre class='src'>
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|100
	[ LIT2 13 -File/length ] DEO2
	INC [ LIT2 -&filename -File/name ] DEO2
	INC SWP .File/write DEO2
	[ LIT2 "4 18 ] DEO
	&filename "4
</pre>
<pre class='cmd'>uxnasm src.tal seed.rom && uxncli seed.rom</pre>
</div>

<img src='../media/refs/the_all_is_one.png' width='120'/>

<p>This <b>polyglot program</b> is both a valid tga image, and a valid rom that will print that same image. It is my entry to  <a href='https://binary.golf/2/' target='_blank'>BGGP2</a></p>

<div class="codeview">
<pre class='src'>
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|100
@length [ 40 ] @header [ 01 01 ]
@color-map [ 0000 3000 20 ] [ 0000 1000 1000 1000 0820 ]
@description $40
@color-map-data [ 0000 00ff ffff ffff $aa !program $b ]
@body [
0101 0101 0101 0100 0001 0101 0101 0101
0101 0101 0101 0000 0000 0101 0101 0101
0101 0101 0100 0000 0000 0001 0101 0101
0101 0101 0100 0101 0101 0001 0101 0101
0101 0101 0100 0101 0101 0001 0101 0101
0101 0101 0100 0001 0100 0001 0101 0101
0101 0101 0101 0001 0100 0101 0101 0101
0101 0101 0000 0101 0101 0000 0101 0101
0101 0100 0101 0101 0101 0101 0001 0101
0101 0100 0100 0101 0101 0101 0001 0101
0101 0100 0100 0101 0101 0001 0001 0101
0101 0100 0101 0001 0101 0001 0001 0101 
0101 0101 0001 0001 0100 0100 0101 0101 
0101 0100 0100 0001 0100 0001 0001 0101 
0101 0001 0101 0101 0101 0101 0100 0101 
0101 0000 0000 0000 0000 0000 0000 0101 ]

@program ( -> )
	( print 2 ) [ LIT2 "2 18 DEO ]
	( | draw tga )
	#0010 DUP2 .Screen/width DEO2
	.Screen/height DEO2
	#0f08 DEOk INC INC DEOk INC INC DEO
	#0000
	&>l ( -- )
		DUP2 #0f AND .Screen/x DEO2
		DUP2 #04 SFT .Screen/y DEO2
		DUP2 ;body ADD2 LDA .Screen/pixel DEO
		INC DUP ?&>l
	POP2
</pre>
<pre class='cmd'>uxnasm src.tal mothra.tga && gimp mothra.tga</pre>
</div>

<img src='../media/refs/mothra.png' alt='mothra' width='16'/>

<p>This program ensnares one into the iconic <a
href='https://www.lemon64.com/forum/viewtopic.php?t=34879'
target='_blank'>Commodore 64 maze</a>:</p>

<div class="codeview">
<pre class='src'>
( seed ) #c5 DEI2 ,&seed STR2
[ LIT2 "/\ ] #f800
&>w ( -- )
	( break ) DUP #3f AND ?{ #0a18 DEO }
	( seed ) OVR2 [ LIT2 &seed &x $1 &y $1 ]
	( randx ) ADDk #50 SFT EOR DUP #03 SFT EOR DUP ,&x STR
	( randy ) SUBk #01 SFT EOR EOR DUP ,&y STR
	( emit ) #01 AND [ LIT POP ] ADD [ #00 STR $1 ] #18 DEO
	INC2 ORAk ?&>w
POP2 POP2
</pre>
<pre class='cmd'>uxnasm src.tal res.rom && uxncli res.rom</pre>
</div>

<p>This program unlocks the scorching power to create <a
href='https://en.wikipedia.org/wiki/COMEFROM' target='_blank'>COMFROM</a>
statements at runtime and prints exclamation marks in an infinite loop:</p>

<div class="codeview">
<pre class='src'>
( 10 ) ;&label COMEFROM 
( 20 ) [ LIT2 "! 18 ] DEO 
( 30 ) &label $4 
( 40 ) BRK

@COMEFROM ( label* -- )
    ( LIT2 ) STH2k [ LIT LIT2 ] STH2kr STA
    ( JMP2 ) INC2r INC2r INC2r [ LIT JMP2 ] STH2r STA
    ( addr* ) STH2kr SWP2 INC2 STA2
    JMP2r
</pre>
<pre class='cmd'>uxnasm src.tal res.rom && uxncli res.rom</pre>
</div>

<p id='bf'>This program compiles caret-prefixed <a
href='https://en.wikipedia.org/wiki/Brainfuck' target='_blank'>Brainfuck</a>
directly into uxntal using nothing but macros:</p>

<div class="codeview">
<pre class='src'>
%>  ( m* -- m* ) { INC2 }
%<  ( m* -- m* ) { #0001 SUB2 }
%^+ ( m* -- m* ) { STH2k LDAk INC STH2r STA }
%^- ( m* -- m* ) { STH2k LDAk #01 SUB STH2r STA }
%^. ( m* -- m* ) { LDAk #18 DEO }
%^[ ( m* -- m* ) { #00 JSR LDAk #00 EQU ?{ }
%^] ( m* -- m* ) { LDAk #00 EQU ?{ JMP2kr }\ }\ }

@on-reset ( -> ) ;memory ^+ ^+ ^+ ^[ ^. ^- ^] ^. BRK
@memory
</pre>
<pre class='cmd'>uxnasm bf.tal res.rom && uxncli res.rom</pre>
</div>

<q style='font-size:28px'>LITr 00 JSRr</q>

<img src='../media/refs/bruno.png' alt="A young girl looks at a boy swinging from a vine. Illustration by Harry Furniss in Caroll's Sylvie and Bruno"/>

<ul>
	<li><a href='https://susam.net/self-printing-machine-code.html' target='_blank'>Self-Printing Machine Code</a></li>
</ul>
<ul></ul></main>
<footer>
	<a href="ethics.html"><img src="../media/icon/3arrows.svg" alt="NoNazis!"/></a>
	<a href="https://webring.xxiivv.com/" target="_blank"><img src="../media/icon/webring.svg" alt="Webring"/></a>
	<a href="https://merveilles.town/@neauoire" rel="me" target="_blank"><img src="../media/icon/merveilles.svg" alt="Merveilles"/></a>
	<a href="uxn.html"><img src="../media/icon/uxn.svg" alt="UxnPowered"/></a> 
	<span><a href="devine_lu_linvega.html">Devine Lu Linvega</a> &copy; 2008-2025</span>
	<div class="right"><a href="about.html#license">BY-NC-SA 4.0</a>
		<a href="https://creativecommons.org/licenses/by-nc-sa/4.0" target="_blank"><img src="../media/icon/cc.svg" alt="CreativeCommons"/></a>
	</div>
	<hr/>
</footer>
</body></html>