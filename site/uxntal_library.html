<!DOCTYPE html><html><head>
<meta charset="utf-8"><meta name="viewport" content="width=device-width, initial-scale=1">
<link href="../links/main.css" type="text/css" rel="stylesheet"><link href="../media/services/icon.png" type="image/png" rel="shortcut icon">
<title>XXIIVV &mdash; uxntal library</title></head><body>
<header><a href="home.html"><img alt="XXIIVV" src="../media/icon/logo.svg"></a></header>

<nav><ul><li><a href="uxntal_syntax.html" class="parent">uxntal syntax</a></li><li><a href="uxntal_stacks.html">uxntal stacks</a></li><li><a href="uxntal_opcodes.html">uxntal opcodes</a></li><li><a href="uxntal_modes.html">uxntal modes</a></li><li><a href="uxntal_immediate.html">uxntal immediate</a></li><li><a href="uxntal_memory.html">uxntal memory</a></li><li><a href="uxntal_devices.html">uxntal devices</a></li><li><a href="uxntal_software.html">uxntal software</a></li></ul><ul><li><a href="uxntal_macros.html">uxntal macros</a></li><li><a href="uxntal_struct.html">uxntal struct</a></li><li><a href="uxntal_scope.html">uxntal scope</a></li><li><a href="uxntal_notation.html">uxntal notation</a></li><li><a href="uxntal_lambdas.html">uxntal lambdas</a></li><li><a href="uxntal_doors.html">uxntal doors</a></li><li><a href="uxntal_signed.html">uxntal signed</a></li><li><a href="uxntal_library.html" class="self">uxntal library</a></li></ul><ul><li><a href="uxntal_labyrinth.html">uxntal labyrinth</a></li></ul></nav>
<main><h2>A collection of commonly used routines in Uxntal projects.</h2>

<p>The following snippets are in the standard format. If you discover faster and smaller helpers, please get in touch with me.</p>

<h3>Hexadecimal Numbers</h3>

<p>To print an hexadecimal number:</p>
<pre>
@&lt;phex&gt; ( short* -: )
	SWP /b
	&b ( byte -: )
		DUP #04 SFT /c
	&c ( byte -: )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
		JMP2r
</pre>

<p>To convert an hexadecimal string to a value:</p>
<pre>
@shex ( str* -: val* )
	[ LIT2r 0000 ]
	&w ( str* `acc* -: val* )
	LDAk chex INC #00 EQU ?{
		[ LITr 40 ] SFT2r LDAk chex [ LITr 00 ] STH
		ADD2r INC2 LDAk ?&w }
	POP2 STH2r JMP2r
</pre>

<p>To convert an hexadecimal character to a nibble:</p>
<pre>
@chex ( c -: val! )
	( dec ) [ LIT "0 ] SUB DUP #09 GTH ?{ JMP2r }
	( hex ) #27 SUB DUP #0f GTH ?{ JMP2r }
	( err ) POP #ff JMP2r
</pre>

<h3>Decimal Numbers</h3>

<p>To print a decimal short to decimal:</p>
<pre>
@pdec ( short* -- )
	#2710 [ LIT2r 00fb ]
	&w ( -- )
		DIV2k #000a DIV2k MUL2 SUB2 SWPr EQUk OVR STHkr EQU AND ?{
			DUP [ LIT "0 ] ADD #19 DEO
			INCr }
		POP2 #000a DIV2 SWPr INCr STHkr ?&w
	POP2r POP2 POP2 JMP2r
</pre>

<p>To print a decimal byte to decimal:</p>

<pre>
@print-dec ( dec -- )
	DUP #64 DIV print-num/try
	DUP #0a DIV print-num/try
	( >> )

@print-num ( num -- )
	#0a DIVk MUL SUB [ LIT "0 ] ADD #18 DEO
	JMP2r
	&try ( num -- )
		DUP ?print-num
		POP JMP2r</pre>

<p>To convert a decimal string to a hexadecimal value.</p>
<pre>
@sdec ( str* -- val* )
	[ LIT2r 0000 ]
	&w ( -- )
		( validate ) LDAk [ LIT "0 ] SUB #09 GTH ?&end
		( accumulate ) [ LIT2r 000a ] MUL2r
		( combine ) LDAk [ LIT "0 ] SUB [ LITr 00 ] STH ADD2r
		( continue ) INC2 LDAk ?&w
	&end POP2 STH2r JMP2r
</pre>

<h3 id='strings'>Strings</h3>

<p>To print a string.</p>
<pre>
@&lt;pstr&gt; ( str* -: )
	LDAk #18 DEO
	INC2 & LDAk ?&lt;pstr&gt;
	POP2 JMP2r
</pre>

<p>Helpers for strings:</p>

<pre>
[TODO]
</pre>

<h3>Memory</h3>

<p>To print an entire page of memory:</p>
<pre>
@pmem ( addr* -- )
	#0000
	&l ( -- )
		ADD2k LDA phex/b
		DUP #0f AND #0f NEQ #16 MUL #0a ADD #18 DEO
		INC NEQk ?&l
	POP2 POP2 JMP2r
</pre>

<p>Helpers for memory.</p>

<pre>
[TODO]
</pre>

<p>Helpers for bitwise operations.</p>

<pre>
@popcnt ( byte -- count ) LITr 00 #00 &w SFTk #01 AND STH ADDr INC SFTk ?&w POP2 STHr JMP2r
</pre>

<h3>Dates</h3>

<p>To find the day of the week from a given date, <a href='https://c-faq.com/misc/zeller.html' target='_blank'>Tomohiko Sakamoto</a>'s method:</p>
<pre>
@dotw ( y* m d -- dotw )
	( y -= m < 3; )
	OVR STH SWP2 #00 STHr #02 LTH SUB2
	STH2
	( t[m-1] + d )
	#00 ROT ;&t ADD2 LDA #00 SWP
	ROT #00 SWP ADD2
	( y + y/4 - y/100 + y/400 )
	STH2kr
	STH2kr #02 SFT2 ADD2
	STH2kr #0064 DIV2 SUB2
	STH2r #0190 DIV2 ADD2
	ADD2
	( % 7 )
	#0007 DIV2k MUL2 SUB2 NIP
	JMP2r
		&t [ 00 03 02 05 00 03 05 01 04 06 02 04 ]
</pre>

<p>To find if a year is a leap year:</p>
<pre>
@is-leap-year ( year* -- bool )
	( leap year if perfectly divisible by 400 )
	DUP2 #0190 ( MOD2 ) DIV2k MUL2 SUB2 #0000 EQU2 ?&leap
	( not a leap year if divisible by 100 )
	( but not divisible by 400 )
	DUP2 #0064 ( MOD2 ) DIV2k MUL2 SUB2 #0000 EQU2 ?&not-leap
	( leap year if not divisible by 100 )
	( but divisible by 4 )
	DUP2 #0003 AND2 #0000 EQU2 ?&leap
	( all other years are not leap years )
	&not-leap
	POP2 #00
	JMP2r
		&leap POP2 #01 JMP2r
</pre>

<h3>Memory</h3>

<pre>
@msfl ( b* a* len* -- )
	STH2
	SWP2 EQU2k ?&end
	&l ( -- )
		DUP2k STH2kr ADD2 LDA ROT ROT STA
		INC2 GTH2k ?&l
	POP2 POP2 &end POP2r JMP2r

@msfr ( b* a* len* -- )
	STH2
	EQU2k ?&end
	&l ( -- )
		DUP2 LDAk ROT ROT STH2kr ADD2 STA
		#0001 SUB2 LTH2k ?&l
	POP2 POP2 &end POP2r JMP2r
</pre>

<h3>Random</h3>

<pre>
@prng-init ( -- )
	[ LIT2 00 -DateTime/second ] DEI
		[ LIT2 00 -DateTime/minute ] DEI #60 SFT2 EOR2
		[ LIT2 00 -DateTime/hour ] DEI #c0 SFT2 EOR2 ,prng/x STR2
	[ LIT2 00 -DateTime/hour ] DEI #04 SFT2
		[ LIT2 00 -DateTime/day ] DEI #10 SFT2 EOR2
		[ LIT2 00 -DateTime/month ] DEI #60 SFT2 EOR2
		.DateTime/year DEI2 #a0 SFT2 EOR2 ,prng/y STR2
	JMP2r

@prng ( -- number* )
	[ LIT2 &x $2 ]
		DUP2 #50 SFT2 EOR2
		DUP2 #03 SFT2 EOR2
	[ LIT2 &y $2 ] DUP2 ,&x STR2
		DUP2 #01 SFT2 EOR2 EOR2
		,&y STR2k POP
	JMP2r
</pre>

<h3>Misc</h3>

<p>To convert a signed byte to a signed short.</p>

<pre>DUP #7f GTH #ff MUL SWP</pre>

<pre>
@smax ( x* y* -> smax* ) EOR2k POP #80 AND ?min !max
@min ( x* y* -> min* ) LTH2k JMP SWP2 POP2 JMP2r
@max ( x* y* -> max* ) LTH2k JMP SWP2 NIP2 JMP2r
@mod ( x y -- z ) DIVk MUL SUB JMP2r
@mod2 ( x* y* -- z* ) DIV2k MUL2 SUB2 JMP2r

( Signed macros )

@abs ( a -- b ) DUP #80 AND #00 EQU ?{ #00 SWP SUB } JMP2r
@abs2 ( a* -- b* ) DUP2k #1f SFT2 MUL2 SUB2 JMP2r
@lts2 ( a* b* -- f ) #8000 STH2k ADD2 SWP2 STH2r ADD2 GTH2 JMP2r
@gts2 ( a* b* -- f ) #8000 STH2k ADD2 SWP2 STH2r ADD2 LTH2 JMP2r

( Binary macros )

@rol ( x y -- z ) DUP #07 SFT SWP #10 SFT ADD JMP2r
@ror ( x y -- z ) DUP #70 SFT SWP #01 SFT ADD JMP2r
@rol2 ( x* y* -- z* ) DUP2 #0f SFT2 SWP2 #10 SFT2 ADD2 JMP2r
@ror2 ( x* y* -- z* ) DUP2 #f0 SFT2 SWP2 #01 SFT2 ADD2 JMP2r
</pre>

<h2>I have reordered time. I have turned the world upside down, and I have done
it all for you!</h2>

<p>This program will emit its own bytecode when run:</p>

<!-- a001 0fa0 0100 9480 1817 21aa 20ff f7 -->

<div class="codeview">
<pre class='src'>
@q ;&end ;q &l LDAk #18 DEO INC2 GTH2k ?&l &end
</pre>
<pre class='cmd'>uxnasm src.tal seed.rom && uxncli seed.rom > res.rom</pre>
</div>

<p>This program will emit a second program that emits its own bytecode as
hexadecimal ascii characters, which is also a valid program source:</p>

<!--
@q ( -> )
	;end ;q
	&l LDA2k phex #2018 DEO INC2 INC2 GTH2k ?&l
	BRK
@phex ( short* -- )
	SWP phex/b
	&b DUP #04 SFT phex/c
	&c #0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
	JMP2r
@end
-->

<div class="codeview">
<pre class='src'>
a001 32a0 0100 b460 000b a020 1817 2121 
aa20 fff2 0004 6000 0006 8004 1f60 0000 
800f 1c06 8009 0a80 271a 1880 3018 8018 
176c
</pre>
<pre class='cmd'>uxnasm src.tal seed.rom && uxncli seed.rom > src.tal</pre>
</div>

<p>This <b>ambigram program</b> executes the same backward or forward, every single opcode is evaluated, and prints the palindrome "tenet". It is my entry to  <a href='https://binary.golf/1/' target='_blank'>BGGP1</a>:</p>

<!--
DEO POP [ LIT2 18 LIT2 ] POP [ LIT2 "t LIT2 ] POP SWP DEO
DEO POP [ LIT2 18 LIT2 ] POP [ LIT2 "e LIT2 ] POP SWP DEO
DEO POP [ LIT2 18 LIT2 ] POP [ LIT2 "n LIT2 ] POP [ LIT2 18 LIT2 ] POP DEO
DEO SWP POP [ LIT2 "e LIT2 ] POP [ LIT2 18 LIT2 ] POP DEO
DEO SWP POP [ LIT2 "t LIT2 ] POP [ LIT2 18 LIT2 ] POP DEO
-->

<div class="codeview">
<pre class='src'>
1702 a018 a002 a074 a002 0417 1702 a018
a002 a065 a002 0417 1702 a018 a002 a06e
a002 a018 a002 1717 0402 a065 a002 a018
a002 1717 0402 a074 a002 a018 a002 17
</pre>
<pre class='cmd'>uxnasm src.tal turnstile.rom && uxncli turnstile.rom</pre>
</div>

<p>This program produces exactly 1 copy of itself, names the copy "4", does not execute the copied file, and prints the number 4. It is my 19 bytes entry to  <a href='https://binary.golf/4/' target='_blank'>BGGP4</a>:</p>

<div class="codeview">
<pre class='src'>
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|100
	[ LIT2 13 -File/length ] DEO2
	INC [ LIT2 -&filename -File/name ] DEO2
	INC SWP .File/write DEO2
	[ LIT2 "4 18 ] DEO
	&filename "4
</pre>
<pre class='cmd'>uxnasm src.tal seed.rom && uxncli seed.rom</pre>
</div>

<img src='../media/refs/the_all_is_one.png' width='120'/>

<p>This <b>polyglot program</b> is both a valid tga image, and a valid rom that will print that same image. It is my entry to  <a href='https://binary.golf/2/' target='_blank'>BGGP2</a></p>

<div class="codeview">
<pre class='src'>
|00 @System &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|100
@length [ 40 ] @header [ 01 01 ]
@color-map [ 0000 3000 20 ] [ 0000 1000 1000 1000 0820 ]
@description $40
@color-map-data [ 0000 00ff ffff ffff 0000 0000 $a6 !program $b ]
@body [
0101 0101 0101 0100 0001 0101 0101 0101
0101 0101 0101 0000 0000 0101 0101 0101
0101 0101 0100 0000 0000 0001 0101 0101
0101 0101 0100 0101 0101 0001 0101 0101
0101 0101 0100 0101 0101 0001 0101 0101
0101 0101 0100 0001 0100 0001 0101 0101
0101 0101 0101 0001 0100 0101 0101 0101
0101 0101 0000 0101 0101 0000 0101 0101
0101 0100 0101 0101 0101 0101 0001 0101
0101 0100 0100 0101 0101 0101 0001 0101
0101 0100 0100 0101 0101 0001 0001 0101
0101 0100 0101 0001 0101 0001 0001 0101 
0101 0101 0001 0001 0100 0100 0101 0101 
0101 0100 0100 0001 0100 0001 0001 0101 
0101 0001 0101 0101 0101 0101 0100 0101 
0101 0000 0000 0000 0000 0000 0000 0101 ]

@program
	( print 2 ) [ LIT2 "2 18 DEO ]
	( | draw picture )
	#f000 DUP2 .System/r DEO2
		DUP2 .System/g DEO2
		.System/b DEO2
	;picture .Screen/addr DEO2
	[ LIT2 16 -Screen/auto ] DEO
	[ LIT2 01 -Screen/sprite ] DEOk DEO
	BRK

@picture [
	0103 0704 0406 020c 80c0 e020 2060 4030
	1014 1412 0a16 203f 0808 2828 5068 04fc ]
</pre>
<pre class='cmd'>uxnasm src.tal mothra.tga && gimp mothra.tga</pre>
</div>

<img src='../media/refs/mothra.png' alt='mothra' width='16'/>

<p>A small program to print the iconic Commodore 64 maze:</p>

<div class="codeview">
<pre class='src'>
( seed ) #c5 DEI2 ,&seed STR2
[ LIT2 "/\ ] #f800
&>w ( -- )
	( break ) DUP #3f AND ?{ #0a18 DEO }
	( seed ) OVR2 [ LIT2 &seed &x $1 &y $1 ]
	( randx ) ADDk #50 SFT EOR DUP #03 SFT EOR DUP ,&x STR
	( randy ) SUBk #01 SFT EOR EOR DUP ,&y STR
	( emit ) #01 AND [ LIT POP ] ADD [ #00 STR $1 ] #18 DEO
	INC2 ORAk ?&>w
POP2 POP2
</pre>
<pre class='cmd'>uxnasm src.tal res.rom && uxncli res.rom</pre>
</div>

<q style='font-size:28px'>LITr 00 JSRr</q>

<img src='../media/refs/bruno.png' alt="A young girl looks at a boy swinging from a vine. Illustration by Harry Furniss in Caroll's Sylvie and Bruno"/>

<ul>
	<li><a href='https://susam.net/self-printing-machine-code.html' target='_blank'>Self-Printing Machine Code</a></li>
</ul>

<style>
	div.codeview { background:#eee; border:2px solid black; border-radius:4px }
	div.codeview iframe { height:405px; border:0; margin:0; display:block }
	div.codeview pre { margin:0; background:#000; color:#fff }
	div.codeview pre.src { background:#efefef; color:black}
	div.codeview pre a { float:right; color:#72dec2; text-decoration:none }
</style>
<ul></ul></main>
<footer><a href="https://webring.xxiivv.com/" target="_blank"><img src="../media/icon/webring.svg" alt="Webring"/></a><a href="https://merveilles.town/@neauoire" rel="me" target="_blank"><img src="../media/icon/merveilles.svg" alt="Merveilles"/></a><a href="ethics.html"><img src="../media/icon/dreipfeile.svg" alt="NoNazis!"/></a><a href="uxn.html"><img src="../media/icon/uxn.svg" alt="UxnPowered"/></a><span> <a href="devine_lu_linvega.html">Devine Lu Linvega</a> &copy; 2008-2024</span><div class="right"><a href="about.html#license">BY-NC-SA 4.0</a><a href="https://creativecommons.org/licenses/by-nc-sa/4.0" target="_blank"><img src="../media/icon/cc.svg" alt="CreativeCommons"/></a></div><hr/></footer>
</body></html>