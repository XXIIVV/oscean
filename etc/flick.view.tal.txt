( build: uxnasm src/view.tal view.rom
| start: uxnemu view.rom )

|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $1 &rl $1 &g $1 &gl $1 &b $1 &bl $1 &debug $1 &state $1
|10 @Console/vector $2 &read $5 &type $1 &write $1 &error $1
|20 @Screen/vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller/vector $2 &button $1 &key $1
|90 @Mouse/vector $2 &x $2 &y $2 &state $1 &pad $3 &sx $2 &sy $1 &sy-lb $1
|a0 @File/vector $2 &success $1 &success-lb $1 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|c0 @DateTime &year $2 &month $1 &day $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1

|000

	@scene/id $1

|100

@on-reset ( -> )
	;meta #06 DEO2
	theme/<load>
	#0140 .Screen/width DEO2
	#00c8 .Screen/height DEO2
	;canvas/on-mouse .Mouse/vector DEO2
	#00 scene/<select-force>
	BRK

@meta 00
	( name ) "Flickview 0a
	( details ) "A 20 "Point-n-click 20 "Viewer 0a
	( author ) "By 20 "Devine 20 "Lu 20 "Linvega 0a
	( date ) "19 20 "Mar 20 "2025 $2

(
@|Scene )

@scene/<jump> ( color -- )
	DUP #01 GTH ?{ POP JMP2r }
	#00 SWP #02 SUB ;canvas/links ADD2 LDA
	( >> )

@scene/<select> ( id -- )
	#0f AND DUP .&id LDZ NEQ ?{ POP JMP2r }
	INCk ?{ POP JMP2r }
	&<select-force> ( id -- )
	.&id STZ
	/<pull>
	;canvas/r LDA .System/rl DEO
	;canvas/g LDA .System/gl DEO
	;canvas/b LDA .System/bl DEO !canvas/<draw>

@scene/<pull> ( -- )
	[ LIT2 00 -&id ] LDZ DUP2 #02 SFT INC ,&src-page STR2
	#03 AND #e0 SFT2 ,&src-addr STR2
	;&cmd-get .System/expansion DEO2
	JMP2r

	&cmd-get [
	01 4000 &src-page $2 &src-addr $2 0000 =canvas/mem ]

(
@|Canvas )

@canvas/on-mouse ( -> )
	cursor/<update>
	.Mouse/state DEI ?{ BRK }
	.Mouse/x DEI2 .Mouse/y DEI2 /get-pixel scene/<jump>
	POP2 POP2 BRK

@canvas/<draw> ( -- )
	[ LIT2 05 -Screen/auto ] DEO
	;&mem .Screen/addr DEO2
	#0000 .Screen/x DEO2
	#0000 .Screen/y DEO2
	#1900
	&>v ( -- )
		#2800
		&>h ( -- )
			[ LIT2 81 -Screen/sprite ] DEO
			INC GTHk ?&>h
		POP2
		( | down )
		#0000 .Screen/x DEO2
		.Screen/y DEI2k #0008 ADD2 ROT DEO2
		INC GTHk ?&>v
	POP2 JMP2r

@get-tile-addr ( x* y* -- x* y* addr* )
	( x ) OVR2 #03 SFT2
	( y ) OVR2 #03 SFT2 #0144 #03 SFT2 MUL2 ADD2
	( ) #40 SFT2 ;canvas/mem ADD2 JMP2r

@canvas/get-pixel ( x* y* -- x* y* color )
	( get tile addr ) get-tile-addr STH2
	( get glyph vertical offset ) DUP2 #0007 AND2 STH2
	ADD2r
	( make bit mask ) OVR2 NIP #07 AND #80 SWP SFT
	( ch1 ) DUP LDAkr STHr AND #00 NEQ SWP
	( ch2 ) STH2r #0008 ADD2 LDA AND #00 NEQ DUP ADD ORA JMP2r

(
@|Cursor )

@cursor/<update> ( -- )
	[ LIT2 12 -Screen/auto ] DEO
	#40 ;fill-icn /<draw>
	[ LIT2 16 -Screen/auto ] DEO
	#c1 ;&down-chr .Mouse/state DEI ?{ POP2 ;&up-chr }
	.Mouse/x DEI2 ,&x STR2
	.Mouse/y DEI2 ,&y STR2
	( >> )

@cursor/<draw> ( color addr* -- )
	.Screen/addr DEO2
	[ LIT2 &x $2 ] #0004 SUB2 .Screen/x DEO2
	[ LIT2 &y $2 ] #0004 SUB2 .Screen/y DEO2
	.Screen/sprite DEOk DEO
	JMP2r

@cursor/up-chr [
	0000 0000 0814 1417 0000 0000 0008 0808
	0000 0000 0000 00c0 0000 0000 0000 0000
	1010 2010 1008 0700 0f0f 1f0f 0f07 0000
	2010 1010 2020 c000 c0e0 e0e0 c0c0 0000 ]

@cursor/down-chr [
	0000 0000 0000 0817 0000 0000 0000 0008
	0000 0000 0000 00c0 0000 0000 0000 0000
	1010 2010 1008 0700 0f0f 1f0f 0f07 0000
	2010 1010 2020 c000 c0e0 e0e0 c0c0 0000 ]

(
@|Theme )

@theme/<reset> ( -- )
	#f0af #f0d7 #f0b6
	( >> )

@theme/<set> ( r* g* b* -- )
	.System/b DEO2
	.System/g DEO2
	.System/r DEO2
	JMP2r

@theme/<load> ( -- )
	;&path .File/name DEO2
	#0002 .File/length DEO2
	;&r .File/read DEO2
	;&g .File/read DEO2
	;&b .File/read DEO2
	.File/success-lb DEI ?{ !theme/<reset> }
	[ LIT2 &r $2 ] [ LIT2 &g $2 ] [ LIT2 &b $2 ] !theme/<set>

	&path ".theme $1

@fill-icn [ ffff ffff ffff ffff ffff ffff ffff ffff ]

@canvas/mem $3e80
	&memcap ( - )
	&links &link1 $1 &link2 $1
	( - ) &r $1 &g $1 &b $1

