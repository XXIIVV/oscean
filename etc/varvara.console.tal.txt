( printf "ghi" | uxncli console.rom "abc" "def" )

|10 @Console/vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1

|100

@on-reset ( -> )
	;on-console .Console/vector DEO2
	BRK

@on-console ( -> )
	.Console/type DEI .Console/read DEI
	( inc ) [ LIT2 &ptr =expect ] INC2k INC2 ,&ptr STR2
	DUP2 ;expect/end EQU2 ?pass
	LDA2 NEQ2 ?fail
	BRK

@fail ( -> )
	;dict/fail str/<print>
	#010f DEO
	BRK

@pass ( type read ptr* -> )
	POP2 POP2 ;dict/pass str/<print>
	#800f DEO
	BRK

(
@|Utils )

@str/<print> ( str* -- )
	LDAk DUP ?{ POP POP2 JMP2r }
	#18 DEO
	INC2 !/<print>

(
@|Data )

@expect [
	02 "a 02 "b 02 "c 03 0a 02 "d 02 "e 02 "f 04 0a
	01 "g 01 "h 01 "i ]
	&end [ 04 0a ]

@dict
	&pass "Console: 20 "pass 0a $1
	&fail "Console: 20 "fail 0a $1

