( It is pitch black. )

|10 @Console/vector $2 &read $5 &type $1 &write $1 &error $1

|000

	@line/buf $80
	@rewrite $1

|100

@on-reset ( -> )
	;line/on-console .Console/vector DEO2
	BRK

(
@|Compiler )

@line/on-console ( -> )
	[ LIT2 0a -Console/read ] DEI
	( | handlers )
	NEQk ?{ /parse POP2 BRK }
	DUP ?{ POP2 BRK }
	/<push>
	POP BRK

@line/parse ( -- )
	/find-spacer INCk ?/<add-rule>
	POP JMP2r

@line/is-spacer ( buf -- buf t )
	LDZ2k LIT2 ":: NEQ2 ?{
		INCk INC LDZ LIT "= NEQ ?{ #01 JMP2r } }
	#00 JMP2r

@line/find-spacer ( -- index )
	,&ptr LDR .&buf
	&>l
		/is-spacer ?{
			INC GTHk ?&>l
		POP #ff }
	NIP JMP2r

@line/<clean> ( -- )
	[ LIT2 -&buf _&ptr ] STR
	[ LIT2 00 -&buf ] STZ
	JMP2r

@line/<push> ( c -- )
	[ LIT2 00 &ptr -&buf ] INCk ,&ptr STR
	STZ2
	JMP2r

@line/<add-rule> ( spacer -- )
	DUP ?{
		POP ;acc/on-console .Console/vector DEO2 !/<clean> }
	rules/<push> !/<clean>

(
@|Accumulator )

@acc/on-console ( -> )
	.Console/read DEI DUP ?{
		POP /<eval>
		#800f DEO
		BRK }
	acc/append BRK

@acc/match ( src* -- src* )
	rules/find INC2k ORA ?{ POP2 LDAk !/append }
	( | walk src )
	LDA2k str/len ROT2 ADD2 SWP2
	( | replace )
	[ LIT2 01 -rewrite ] STZ
	INC2 INC2 LDA2
	&>wa
		LDAk DUP ?{ POP POP2 !/<fill> }
		/append INC2 !&>wa

@acc/append ( c -- )
	#00 [ LIT2 &dst =&buf-1 ] INC2k ,&dst STR2
	STA2
	JMP2r

@acc/get-src ( -- src* )
	[ LIT2 &src $2 ] JMP2r

@acc/<swap> ( -- )
	;&buf-1 ;&buf-2 [ LIT2 01 &flip $1 ] INCk ,&flip STR
	AND [ JMP SWP2 ] ,&src STR2
	,&dst STR2
	/get-src !str/<print>

@acc/<fill> ( str* -- str* )
	&>wf
		LDAk DUP ?{ POP JMP2r }
		/append INC2 !&>wf

@acc/<eval> ( -- )
	/<swap>
	&>we
		[ LIT2 00 -rewrite ] STZ
		/get-src
		&>ws
			/match INC2 LDAk ?&>ws
		POP2 /<swap>
		.rewrite LDZ ?&>we
	JMP2r

(
@|Rules )

@rules/find ( src* -- src* rule* )
	STH2k ,&ptr LDR2 ;&buf
	&>lf
		LDA2k STH2kr /compare ?{
			#0004 ADD2 GTH2k ?&>lf
		POP2 #ffff }
	NIP2 POP2r JMP2r

@rules/compare ( rule* src* -- t )
	STH2
	&>wc
		LDAk LDAkr STHr NEQ ?{ INC2 INC2r LDAk ?&>wc }
	POP2r LDA #00 EQU JMP2r

@rules/<push> ( spacer -- )
	#00 OVR STZ
	( rhs ) INC INC INC dict/alloc
	( lhs ) .line/buf dict/alloc [ LIT2 &ptr =&buf ] STH2k STA2
	INC2r INC2r STH2kr STA2
	INC2r INC2r [ LITr _&ptr ] STR2r
	JMP2r

(
@|Dict )

@dict/alloc ( str -- addr* )
	,&ptr LDR2 ROT
	&>w
		LDZk DUP ?{ POP2 #00 !/<push> }
		/<push>
		INC !&>w

@dict/<push> ( c -- )
	[ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
	STA
	JMP2r

(
@|Utils )

@str/<print> ( str* -- )
	&>wp
		LDAk DUP ?{ POP POP2 JMP2r }
		.Console/write DEO
		INC2 !&>wp

@str/len ( str* -- length* )
	DUP2 /cap SWP2 INC2 SUB2 JMP2r

@str/cap ( str* -- end* )
	LDAk ?{ INC2 JMP2r }
	INC2 !/cap

@dict/buf $1000

@rules/buf $200

@acc/buf-1 $6000 &buf-2

