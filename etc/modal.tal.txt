( Of, or relating to structure, as opposed to substance. )

|10 @Console/vector $2 &read $5 &type $1 &write $1 &error $1

|000

	@token/buf

|100

@on-reset ( -> )
	;token/on-console .Console/vector DEO2
	;prefabs/c dict/<push-str>
	BRK

%NEXT ( addr* -- next* ) {
	INC2 INC2 }

@walk ( addr* -- next* )
	[ LITr 00 ]
	&>w
		LDA2k ORA ?{ POPr JMP2r }
		LDA2k ;CellType/a NEQ2 ?{ INCr }
		LDA2k ;CellType/b NEQ2 ?{
			[ LITr 01 ] SUBr STHkr ?{ POPr NEXT JMP2r } }
		NEXT STHkr ?&>w
	POPr JMP2r

@<eval> ( -- )
	<step>
	body/<print>
	wildcards/<output>
	?<eval>
	JMP2r

@<step> ( -- rew )
	[ LITr 00 ] body/<swap>
	&>l
		LDA2k ;CellType/a NEQ2 ?{
			DUP2 rules/find [ INC2k ORA ?/<write-match> POP2 ] }
		LDA2k ;dict/buf EQU2 ?/<write-rule>
		LDA2k body/<push-cell>
		NEXT GTH2k ?&>l
	( >> )
	&<write-tail> ( to* from* . rew -- rew )
	NEQ2k ?{
		POP2 #0000 SWP2 STA2
		STHr JMP2r }
	LDA2k body/<push-cell>
	NEXT !/<write-tail>

	&<write-match> ( to* from* . rew -- rew )
	INCr
	( rewrite ) walk <write-rhs>
	( advance ) walk !/<write-tail>

	&<write-rule> ( to* from* . rew -- rew )
	INCr NEXT rules/<push> !/<write-tail>

@<write-rhs> ( tail* rhs* -- )
	DUP2 walk SWP2
	&>l
		<write-cell>
		NEXT GTH2k ?&>l
	POP2 POP2 JMP2r

@<write-cell> ( addr* -- addr* )
	LDA2k LDA [ LIT "? ] EQU ?&<write-wildcard>
	LDA2k !body/<push-cell>

	&<write-wildcard> ( addr* -- addr* )
	LDA2k wildcards/find INC2k ORA ?{ POP2 JMP2r }
	NEXT LDA2 DUP2 walk SWP2
	&>l
		LDA2k body/<push-cell>
		NEXT GTH2k ?&>l
	POP2 POP2 JMP2r

(
@|token )

@token/on-console ( -> )
	.Console/read DEI DUP ?{ POP <eval>
		BRK }
	/<push>
	BRK

@token/<push> ( c -- )
	( | handlers )
	DUP [ LIT "( ] NEQ ?{ /<store>
		;CellType/a !body/<push-cell> }
	DUP [ LIT ") ] NEQ ?{ /<store>
		;CellType/b !body/<push-cell> }
	DUP #21 LTH ?/<store>
	[ LIT2 00 &ptr -&buf ] INCk ,&ptr STR
	STZ2
	JMP2r

@token/<store> ( c -- )
	POP [ LIT2 -&buf _&ptr ] LDR NEQ ?{ JMP2r }
	;&buf dict/find-alloc body/<push-cell>
	[ LIT2 00 -&buf ] DUP ,&ptr STR
	STZ
	JMP2r

(
@|rules )

@rules/compare ( rule* cells* -- a* bool cells* )
	STH2
	LDA2k LDA [ LIT "? ] EQU ?&wildcard
	LDA2k STH2kr LDA2 EQU2 STH
	NEXT STHr STH2r NEXT JMP2r

	&wildcard ( rule* cells* -- a* bool cells* )
	STH2kr LDA2 ;CellType/b NEQ2 ?{ #00 STH2r JMP2r }
	( | find wildcard )
	LDA2k wildcards/find INC2k ORA ?{
		POP2 STH2kr OVR2 LDA2 wildcards/<push>
		NEXT #01 STH2r !walk }
	( | compare wildcard )
	STH2kr wildcards/compare STH
	NEXT STHr STH2r !walk

@rules/match ( rule* cells* -- bool )
	wildcards/<reset>
	STH2
	DUP2 walk SWP2
	&>lmn
		STH2r /compare STH2
		?{ EQU2 POP2r JMP2r }
		GTH2k ?&>lmn
	EQU2 POP2r JMP2r

@rules/find ( cells* -- rule* )
	STH2
	,&ptr LDR2 ;&buf
	&>lm
		DUP2 STH2kr /match ?{
			walk walk GTH2k ?&>lm
		POP2 #ffff }
	NIP2 POP2r JMP2r

@rules/<push> ( addr* -- addr* )
	DUP2 walk walk SWP2
	&>l
		LDA2k [ LIT2 &ptr =&buf ] INC2k INC2 ,&ptr STR2
		STA2
		NEXT GTH2k ?&>l
	POP2 JMP2r

(
@|body )

@body/<push-cell> ( cell* -- )
	[ LIT2 &ptr =&a ] INC2k INC2 ,&ptr STR2
	STA2
	JMP2r

@body/<swap> ( -- to* from* )
	[ LIT2 01 &bank $1 ] INCk ,&bank STR
	AND ?{
		;&ptr LDA2 ;&a DUP2 ,&origin STR2
		;&b ;&ptr STA2
		JMP2r }
	;&ptr LDA2 ;&b DUP2 ,&origin STR2
	;&a ;&ptr STA2
	JMP2r

@body/<print> ( -- )
	[ LIT2 "( _&last ] STR
	[ LIT2 &origin $2 ]
	&>lp
		LDA2k ORAk ?{
			POP2 POP2 #0a19 DEO
			JMP2r }
		LDAk [ LIT ") ] EQU [ LIT2 &last $1 "( ] EQU ORA ?{ #2019 DEO }
		LDAk ,&last STR
		<print-string>
		NEXT !&>lp

(
@|wildcards )

@wildcards/compare ( wildcard* cells* -- f )
	SWP2 INC2 INC2 LDA2 STH2
	DUP2 walk SWP2
	&>lc
		LDA2k STH2kr LDA2 EQU2 ?{ EQU2 POP2r JMP2r }
		( | compare )
		NEXT INC2r INC2r GTH2k ?&>lc
	EQU2 POP2r JMP2r

@wildcards/find ( name* -- wildcard* )
	STH2
	,&ptr LDR2 ;&buf
	&>l
		LDA2k STH2kr scmp ?{
			#0004 ADD2 GTH2k ?&>l
		POP2 #ffff }
	NIP2 POP2r JMP2r

@wildcards/<push> ( addr* name* -- )
	( | save )
	[ LIT2 &ptr =&buf ] STA2k
	( ) NIP2 NEXT STA2k
	( ) NEXT ,&ptr STR2
	POP2 JMP2r

@wildcards/<reset> ( -- )
	;&buf ,&ptr STR2
	#0000 ;&buf STA2
	JMP2r

@wildcards/<output> ( -- )
	;&key /find INC2k ORA ?{ POP2 JMP2r }
	NEXT LDA2 LDA2
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		#18 DEO
		INC2 !&>w

	&key "?: $1

(
@|dict )

@dict/find ( str* -- dict* )
	STH2
	,&ptr LDR2 ;&buf
	&>l
		DUP2 STH2kr scmp ?{
			INC2 GTH2k ?&>l
		POP2 #ffff }
	NIP2 POP2r JMP2r

@dict/find-alloc ( str* -- cell* )
	DUP2 /find INC2k ORA ?{ POP2 !/alloc }
	NIP2 JMP2r

@dict/alloc ( str* -- cell* )
	,&ptr LDR2 SWP2
	( >> )

@dict/<push-str> ( str* -- )
	LDAk DUP [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
	STA
	?{ POP2 JMP2r }
	INC2 !/<push-str>

(
@|utils )

@<print-string> ( str* -- )
	LDAk DUP ?{ POP POP2 JMP2r }
	#19 DEO
	INC2 !<print-string>

@scmp ( a* b* -- bool )
	STH2
	&>l
		LDAk #20 GTH ?{ &d LDA LDAr STHr EQU JMP2r }
		LDAk LDAkr STHr NEQ ?&d
		INC2 INC2r !&>l

(
@|assets )

@prefabs/c "<> $1
	&a "( $1
	&b ") $1

@wildcards/buf $100

@rules/buf $1000

@dict/buf $1000

@body/a $4000 &b $4000
|prefabs/a @CellType/a
|prefabs/b @CellType/b

