( uxncli sym.rom file.bin.sym )

|10 @Console/vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1
|a0 @File/vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|000

	@src/buf $40

|100

@on-reset ( -> )
	.Console/type DEI ?{
		;Dict/usage str/<print>
		#010f DEO
		BRK }
	;src/on-console .Console/vector DEO2
	BRK

@src/on-console ( -> )
	[ LIT2 02 -Console/type ] DEI EQU ?{ <parse>
		#800f DEO
		BRK }
	.Console/read DEI [ LIT &ptr -&buf ] INCk ,&ptr STR
	STZ
	BRK

@<parse> ( -- )
	;src/buf .File/name DEO2
	#8000 .File/length DEO2
	;sym DUP2 .File/read DEO2
	.File/success DEI2 ADD2 ;sym
	&>l
		LDA2k hex/<print>
		#2018 DEO
		INC2 INC2 DUP2 str/<print>
		#0a18 DEO
		str/cap INC2 GTH2k ?&>l
	POP2 POP2 JMP2r

@hex/<print> ( short* -- )
	SWP /b
	&b ( -- )
	DUP #04 SFT /c
	&c ( -- )
	#0f AND DUP #09 GTH #27 MUL ADD LIT "0 ADD #18 DEO
	JMP2r

@str/cap ( str* -- end* )
	LDAk ?{ JMP2r }
	INC2 !/cap

@str/<print> ( str* -- )
	&>w
		LDAk DUP ?{ POP POP2 JMP2r }
		#18 DEO
		INC2 !&>w

@Dict
	&usage "usage: 20 "sym.rom 20 "input.bin.sym 0a $1

@sym

